#+STARTUP: show2levels
* torgeir/.doom.d
:PROPERTIES:
:header-args:emacs-lisp: :lexical t :results silent
:END:

Managing [[https://github.com/torgeir/.emacs.d/tree/d1c9dec646824ab7396b8a35fd7da1416a0580d3][a plain emacs config]] that got closer and closer to what you get ootb with [[https://github.com/doomemacs/doomemacs][doom emacs]] was beginning to feel tedious, so this is an attempt to for a setup with ~feature parity on top of doom emacs. It was probably due a clean up after all these years.

** Install

First, install doom emacs.

#+begin_src sh :tangle no :results no
git clone --depth 1 https://github.com/doomemacs/doomemacs ~/.emacs.d
~/.emacs.d/bin/doom install
#+end_src

Then the config.

#+begin_src sh :tangle no :results no
cd && git clone git@github.com:torgeir/.emacs.d.git ~/.doom.d
#+end_src

And some terminal shortcuts

#+begin_src sh :tangle no :results no
ln -sf ~/.doom.d/emacsclient ~/bin/e
ln -sf ~/.doom.d/emacsclient-terminal ~/bin/em
#+end_src

There were native comp issues on emacs-plus@28 after upgrading to ventura on mac. Solved by [[https://github.com/d12frosted/homebrew-emacs-plus/issues/485#issuecomment-1308164501][linking libgccjit in the brew lib folder]].

** Doom help

The built in emacs help is fantastic. The doom one on ~SPC h d~ is even better! Here's a few examples.

- [[file:~/.emacs.d/docs/examples.org][Browse examples of useful apis]]
- [[elisp:(call-interactively 'doom/help-modules)][Lookup help for doom modules]]
- [[elisp:(call-interactively 'doom/help-search)][Search for in the doom .emacs.d folder]]
- ~SPC h d h~ to access documentation
- ~SPC h d~ followed by ~C-h~ invokes a fuzzy search of keys available in the formerly pressed binding. This also works with ~SPC~ followed by ~C-h~.
- ~K~ on a module to view its documentation
- ~gd~ on a module to browse its directory

** Literate config

This config uses the =:config literal= that tangles this file to ~config.el~ on save. The [[file:init.el::setq +literate-config-file (concat (getenv "HOME") "/.doom.d/readme.org")][+literate-config-file]] is set in ~init.el~ and points to this file.

*** Configuration inspiration

- https://github.com/zzamboni/dot-doom/blob/master/doom.org
- https://abdelhakbougouffa.pro/posts/config/
 
*** Navigation

I usually navigate this with =c-c c-n= or =c-c c-p=, or =mgg=.

** Packages

This is tangled to ~packages.el~ on save.

#+begin_src emacs-lisp :tangle packages.el
(package! evil-cleverparens)
(package! transpose-frame)
(package! highlight-symbol)
(package! olivetti)
(package! focus)
(package! calendar-norway)
(package! spray)
(package! dired-sidebar)
(package! dired-subtree)
(package! org-ai :recipe (:host github :repo "rksm/org-ai"))
(package! mastodon)
(package! hnreader)
(package! reddigg)
(package! docker-tramp)
(package! blamer
  :recipe (:host github :repo "artawower/blamer.el")
  :pin "99b43779341af0d924bfe2a9103993a6b9e3d3b2")
(package! org-appear
  :recipe (:host github :repo "awth13/org-appear")
  :pin "60ba267c5da336e75e603f8c7ab3f44e6f4e4dac")
(package! remark-mode)
;; (package! tempel)
(package! command-log-mode)
;; fix broken doom install for thesaurus on a mac?
(package! wordnut :pin "feac531404041855312c1a046bde7ea18c674915")
;; fix broken doom install for thesaurus on a mac?
(package! synosaurus :pin "14d34fc92a77c3a916b4d58400424c44ae99cd81")
(package! recursion-indicator)
#+end_src

** Lexical scoping everywhere

The madness that is dynamic is too much for anyone, really.

#+begin_src emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src

** Recentf

Ignore some of the cached emacs files in recent files

#+begin_src emacs-lisp
(after! recentf
  (add-to-list 'recentf-exclude "\.emacs\.d/\.local"))
#+end_src

** Whoami

#+begin_src emacs-lisp
(let ((email   (getenv "USER_EMAIL"))
      (email-2 (getenv "USER_EMAIL_2")))
  (when (not email)   (error "No USER_EMAIL set?"))
  (when (not email-2) (error "No USER_EMAIL_2 set?"))
  (setq user-full-name "Torgeir Thoresen"
        user-mail-address   email
        user-mail-address-2 email-2))
#+end_src

** 1password

#+begin_src emacs-lisp
(defun t/1p (item &optional args)
  "Lookup 1p item. On linux, sign in manually first."
  (let ((args (or args "--fields label=password")))
    (if is-mac
        (with-temp-buffer
          (if (zerop (call-process-shell-command (format "op item get %s %s" item args) nil t))
              (replace-regexp-in-string (rx "\n" eos) "" (buffer-string))
            (error "1p: looking up item failed.")))
      (let* ((pass (read-passwd "1p master password: "))
             (session-token nil)
             (ret nil))
        (setq session-token (with-temp-buffer
                              (if (zerop (call-process-shell-command (format "echo -n %s | op signin --raw" pass) nil t))
                                  (replace-regexp-in-string (rx "\n" eos) "" (buffer-string))
                                (error "1p: auth failed."))))
        (with-temp-buffer
          (if is-linux
              (call-process-shell-command (format "op --session %s item get %s %s" session-token item args) nil t))
          (replace-regexp-in-string (rx "\n" eos) "" (buffer-string)))))))
#+end_src

** gpg

#+begin_src emacs-lisp
(defun t/gpg ()
  (interactive)
  (shell-command
   (format
    "gpg-connect-agent updatestartuptty /bye > /dev/null && \
       $(gpgconf --list-dirs libexecdir)/gpg-preset-passphrase -c -P '%s' \
       $(gpg --fingerprint --with-keygrip torgeir@keybase.io | awk '/Keygrip/ {print $3}' | tail -n 1) && \
       gpg -q --batch -d ~/.authinfo.gpg > /dev/null"
    (t/1p "keybase.io" "--format json | jq -j '.fields[] | select(.id == \"password\") | .value'"))))
#+end_src

** Org file location setup

#+begin_quote
;; If you use `org' and don't want your org files in the default location below,
;; change `org-directory'. It must be set before org loads!
#+end_quote

#+begin_src emacs-lisp
(setq org-directory (expand-file-name "~/Dropbox/org/")
      org-agenda-files '("~/Dropbox/org" "~/Dropbox/org/IFTTT/twitter.txt" "~/Dropbox/org/IFTTT/pocket.txt" )
      org-archive-location "%s_archive.gpg::") ; so files are encrypted automatically
#+end_src

** t-defuns

My old collection of more or less useful defuns.

#+begin_src emacs-lisp
(progn
  (defconst is-win IS-WINDOWS)
  (defconst is-cygwin IS-WINDOWS)
  (defconst is-mac IS-MAC)
  (defconst is-linux IS-LINUX)
  (defun t/user-file (path) (concat (expand-file-name "~/") path))
  (defun t/user-emacs-file (path) (concat (expand-file-name "~/.doom.d/") path))
  (defun t/user-dropbox-folder (path) (expand-file-name (concat "~/Dropbox" (if is-mac " (Personal)" "") "/" path)))
  (load! "./t-defuns.el"))
#+end_src

** Micro state

A small overlay map that exposes a set of key bindings until you press q, or something else not in the keymap.

#+begin_src emacs-lisp
(defun t/micro-state (quit key fn &rest bindings)
  "Micro state that temporarily overlays a new key map, kinda like hydra"
  (let ((keymap (make-sparse-keymap)))
    (while key
      (bind-key key fn keymap)
      (setq key (pop bindings)
            fn (pop bindings)))
    (lambda ()
      (interactive)
      (let ((exit (set-temporary-overlay-map keymap t (lambda () (when quit (quit-window))))))
        (when quit
          (bind-key "q" (cmd! (funcall exit)) keymap))))))
#+end_src

And one that enters a ~mode~, then turns on the keymap. It turns ~mode~ off again if you hit a key not in the map.

#+begin_src emacs-lisp
(defun t/micro-state-in-mode (mode after key fn &rest bindings)
  "Micro state that toggles mode and temporarily overlays a new key map, kinda like hydra"
  (let ((keymap (make-sparse-keymap)))
    (while key
      (bind-key key fn keymap)
      (setq key (pop bindings)
            fn (pop bindings)))
    (lambda ()
      (interactive)
      (funcall mode)
      (set-temporary-overlay-map keymap t (lambda nil
                                            (funcall mode -1)
                                            (when after (after)))))))
#+end_src

** Editor

*** Flash on long jumps

Use =nav-flash= to flash the cursor after long jumps.

#+begin_src emacs-lisp
(after! nav-flash
  (custom-set-faces!
    '(nav-flash-face :background "maroon4")
    '(nav-flash-face :background "#003366"))
  (setq nav-flash-delay 4
        nav-flash-use-pulse 'gui-only))
#+end_src

*** Recursive minibuffers

When you change your mind and need to do something first, after you already started a command that opens the minibuffer.

#+begin_src emacs-lisp
(setq enable-recursive-minibuffers t)
#+end_src

And a slightly fancier indicator than ~(minibuffer-depth-indicate-mode)~

#+begin_src emacs-lisp
(use-package! recursion-indicator
  :config
  (recursion-indicator-mode))
#+end_src

*** Initialization

#+begin_src emacs-lisp
(defun +daemon-startup ()
  ;; mu4e
  (when (require 'mu4e nil t)
    ;; Automatically start `mu4e' in background.
    (when (load! "mu-lock.el" (expand-file-name "email/mu4e/autoload" doom-modules-dir) t)
      (setq +mu4e-lock-greedy t
            +mu4e-lock-relaxed t)
      (when (+mu4e-lock-available t)
        ;; Each 5m, if `mu4e' if closed, start it in background.
        (run-at-time nil ;; Launch now
                     (* 5 60)
                     (lambda ()
                       (when (and (not (mu4e-running-p)) (+mu4e-lock-available))
                         (mu4e--start)
                         (message "Started `mu4e' in background.")))))))

  ;; rss
  (when (require 'elfeed nil t)
    (run-at-time nil (* 2 60 60) #'elfeed-update)))

(when (daemonp)
  (add-hook 'emacs-startup-hook #'+daemon-startup)
  (add-hook 'server-after-make-frame-hook #'doom/reload-theme))
#+end_src

In daemon mode history is not saved and recent files is not updated, so help

#+begin_src emacs-lisp
(when (daemonp)
  (add-hook! '(delete-frame-functions delete-terminal-functions)
    (defun t/help-save-history-in-daemon ()
      (let ((inhibit-message t))
        (recentf-save-list)
        (savehist-save)))))
#+end_src

*** Auth sources

Move ~~/.authinfo.gpg~ to the front. It is originally behind the macos keychain that doom puts in there.

#+begin_src emacs-lisp
(after! auth-source (setq auth-sources (nreverse auth-sources)))
#+end_src

When starting Emacs in daemon mode, ask for a valid passphrase in the gpg-agent.

#+begin_src emacs-lisp
(when (daemonp)
  (let ((try-again 3)
        unlocked)
    (while (not (or unlocked
                    (zerop try-again)))
      (setq unlocked (zerop (shell-command "gpg -q --no-tty --logger-file /dev/null --batch -d ~/.authinfo.gpg > /dev/null" nil nil))
            try-again (1- try-again))
      (unless unlocked (message "GPG: failed to unlock, please try again (%d)" try-again)))
    (unless unlocked (kill-emacs 1))))
#+end_src

Defaults

#+begin_src emacs-lisp
(let ((h (* 4 60 60)))
  (setq auth-source-do-cache t
        auth-source-cache-expiry h
        password-cache t
        password-cache-expiry h))

(after! epa
  (setq-default epa-file-encrypt-to '("torgeir@keybase.io")))
#+end_src


Wait just long enough.

#+begin_src emacs-lisp
(setq which-key-idle-delay 0.5
      which-key-idle-secondary-delay 0.05)
#+end_src

*** Disable annoying defaults

Reset [[file:~/.emacs.d/modules/config/default/config.el::(map! "<M-up>" #'drag-stuff-up][drag stuff on meta arrows]], ~m-left/right~ is too engrained to move between words.

#+begin_src emacs-lisp
;; (add-to-list 'drag-stuff-except-modes 'org-src-mode)
(add-hook! 'doom-after-init-modules-hook
  (defun t/unbind-drag-stuff ()
    (map! "<M-left>"  nil
          "<M-right>" nil)))
#+end_src

Remove highlight indent guides by default

#+begin_src emacs-lisp
(remove-hook! '(prog-mode-hook text-mode-hook conf-mode-hook) 'highlight-indent-guides-mode)
#+end_src

Soft wrap everywhere

#+begin_src emacs-lisp
(add-hook! 'doom-after-init-hook
  (defun t/after-init-hook ()
    (global-emojify-mode -1)
    (global-visual-line-mode 1)))
#+end_src

Truncate lines everywhere

#+begin_src emacs-lisp
(add-hook! '(prog-mode-hook text-mode-hook conf-mode-hook)
  (defun t/prog-mode-hook ()
    (interactive)
    (toggle-truncate-lines 0)
    (toggle-truncate-lines 1)))
#+end_src

*** Whitespace

#+begin_src emacs-lisp
;;(add-to-list 'whitespace-style 'trailing)
(add-hook!
 'prog-mode-hook
 (defun t/set-whitespace-style ()
   (interactive)
   (setq whitespace-style '(face tabs trailing lines ;; space-mark spaces
                             space-before-tab newline indentation
                             empty space-after-tab tab-mark
                             newline-mark missing-newline-at-eof))))
#+end_src

*** Evil

I spent so much time with vim, I will probably never give it up.

**** Config

Useful for ~c-e~ followed by ~c-x c-e~ to eval a s-exp. Makes ~cleverparens~ nav commands like ~L~ and ~H~ move across sexps

#+begin_src emacs-lisp
(setq evil-move-beyond-eol t)
#+end_src

Don't use zz and zq for org src editing

#+begin_src emacs-lisp
(after! evil-collection
  (add-to-list 'evil-collection-key-blacklist "ZZ")
  (add-to-list 'evil-collection-key-blacklist "ZQ"))
#+end_src

Fine undo

#+begin_src emacs-lisp
(after! evil
  (setq evil-want-fine-undo t))
#+end_src

**** Unbind C-h in evil window bindings

I use ~SPC w h~ instead of ~SPC w C-h~ to move to the left window. ~C-h~ is more useful as =embark-prefix-help-command=, which this falls back to, like in all other keymaps

#+begin_src emacs-lisp
(map! :after evil :map evil-window-map "C-h" nil)
#+end_src

Try e.g. ~SPC C-h~ to browse all available commands with vertico.

**** Increment & Decrement number

#+begin_src emacs-lisp
(map! :n "g-" #'evil-numbers/dec-at-pt
      :n "g+" #'evil-numbers/inc-at-pt)
#+end_src

**** Registers

Some macros I once used.

This one makes camelCaseWords into to snake_case_words. Run it with ~@c~

#+begin_src emacs-lisp
(evil-set-register ?c [?: ?s ?/ ?\\ ?\( ?\[ ?a ?- ?z ?0 ?- ?9 ?\] ?\\ ?\) ?\\ ?\( ?\[ ?A ?- ?Z ?0 ?- ?9 ?\] ?\\ ?\) ?/ ?\\ ?1 ?_ ?\\ ?l ?\\ ?2 ?/ ?g])
#+end_src

*** Macros

A useful macro one for testing stuff out

#+begin_src emacs-lisp
(defmacro comment (&rest ignore)
  nil)

(comment
 (funcall (t/micro-state nil "m" (cmd! (message "1")))))
#+end_src

*** Embark

#+begin_src emacs-lisp
(map!
 :g "C-," #'embark-act ; global
 :map org-mode-map "C-," #'embark-act
 :map minibuffer-mode-map "C-," #'embark-act)
#+end_src

Prevent =embark-export=, ~C-e~, from being "popupized" by doom's ~:ui popup~ and its ~(popup +all)~ setting.

#+begin_src emacs-lisp
(set-popup-rule! "^*Embark" :ignore t)
#+end_src

You can use C-SPC to preview candidates.

Embark improves prefix help commands, e.g. ~C-c C-h~, by showing auto complete that is fuzzy searchable.

Sometimes its useful not to close it. Hit ~q~ after opening it to ~embark-toggle-quit~ before e.g. running ~k~ to kill a buffer. Or use this with ~m-x~

#+begin_src emacs-lisp
(after! embark
  (defun embark-act-noquit ()
    "Run action but don't quit the minibuffer afterwards."
    (interactive)
    (let ((embark-quit-after-action nil))
      (embark-act))))
#+end_src

*** Vertico

~C-a c-k~ is so engrained in my fingers, I need it everywhere. ~C-a~ seems to work out of the box.

#+begin_src emacs-lisp
(after! vertico
  (map! :map vertico-map
        :g "C-k" 'kill-line))
#+end_src

Exclude stuff from ~+default/search-project~ by placing excludes in [[file:~/.rgignore][~/.rgignore]]

*** Eldoc

Disable eldoc on the modeline, makes it so eldoc only appears on ~SPC h .~, i.e. on  ~m-x eldoc-doc-buffer~

#+begin_src emacs-lisp
(add-hook! '(web-mode js-mode rjsx-mode typescript-mode typescript-tsx-mode)
  (defun t/eldoc-only-in-buffer ()
    (interactive)
    (setq eldoc-message-function (defun t-void (&optional one two) nil))))
#+end_src

*** Orderless

A tuned version of [[https://protesilaos.com/codelog/2021-01-06-emacs-default-completion/][Prot's]] and [[https://kristofferbalintona.me/posts/202202211546/][Kristoffer Balintona's]] vertico, maginalia and orderless setup

Some examples and explanations

- m-x: name= ^[m] :: contains chars of name in word in order AND starts with regex m
- m-x: Buffer= e nm= :: contains chars of Buffer in word in order AND contains e AND contains chars of nm in word in order (e.g. like in u<nm>ark)
- SPC s p: #defun#j gjp, ha, :: rg search for defun, in-emacs matching for long words that have leading inner words starting with g j and p in order, and have leading inner words starting with h and a

#+begin_src emacs-lisp
(after! orderless

  (setq marginalia-max-relative-age 0)

  (progn

    (setq orderless-matching-styles
          '(orderless-literal
            ;; orderless-initialism
            ;; orderless-regexp
            ;; orderless-flex
            ))

    (setq orderless-style-dispatchers
          '(initialism-dispatcher ;; suffix search with =
            flex-dispatcher       ;; suffix search with .
            regexp-dispatcher     ;; suffix search with ~
            or-regexp             ;; regex search with foo|bar
            ))

    (defun regexp-dispatcher (pattern _index _total)
      "Matches regexp."
      (when (string-suffix-p "~" pattern)
        `(orderless-regexp . ,(substring pattern 0 -1))))

    (defun flex-dispatcher (pattern _index _total)
      "Matches using any group in any order."
      (when (string-suffix-p "." pattern)
        `(orderless-flex . ,(substring pattern 0 -1))))

    (defun or-regexp (pattern index _total)
      "foo|bar"
      (cond
       ((string-suffix-p "|" pattern)
        `(orderless-regexp . ,(concat "\\(" (concat (s-replace "|" "\\|" (substring pattern 0 -1)) "\\)"))))
       ((string-match-p "|" pattern)
        `(orderless-regexp . ,(concat "\\(" (concat (s-replace "|" "\\|" pattern) "\\)"))))))

    (defun literal-dispatcher (pattern _index _total)
      "Literal style dispatcher using the equals sign as a suffix."
      (when (string-suffix-p "=" pattern)
        `(orderless-literal . ,(substring pattern 0 -1))))

    ;;;###autoload
    (defun initialism-dispatcher (pattern _index _total)
      "Matches leading on words in order
E.g.
#fun#gjp, ha,
(defun t/js2-get-json-path (&optional hardcoded-array-index))
 ^^^^^       ^   ^    ^               ^         ^
#fun#gjp, hi,
Would not match the above as no leading words start h then another word starting with i
"
      (when (string-suffix-p "," pattern)
        `(orderless-strict-initialism . ,(substring pattern 0 -1))))

    (defun orderless-strict-initialism (component)
      "Match a COMPONENT as a strict initialism, optionally ANCHORED.
The characters in COMPONENT must occur in the candidate in that
order at the beginning of subsequent words comprised of letters.
Only non-letters can be in between the words that start with the
initials.

If ANCHORED is `start' require that the first initial appear in
the first word of the candidate.  If ANCHORED is `both' require
that the first and last initials appear in the first and last
words of the candidate, respectively."
      (orderless--separated-by
          '(seq (zero-or-more alpha) word-end (zero-or-more (not alpha)))
        (cl-loop for char across component collect `(seq word-start ,char))))))
#+end_src
*** Editing

Iterate through CamelCase words

#+begin_src emacs-lisp
(global-subword-mode 1)
#+end_src

*** +onsave

The built in ~format-all-mode~ is enough, don't need ~eglot~ formatting as well. It messes up prettier.

#+begin_src emacs-lisp
(setq +format-with-lsp nil)
#+end_src

*** Dired

#+begin_src emacs-lisp
(after! all-the-icons
  ;; fixes wierd issue when emacs looses focus
  (setq all-the-icons-dired-monochrome t))
(after! dired
  (add-hook 'dired-mode-hook 'dired-subtree-toggle)
  (add-hook 'dired-mode-hook 'dired-hide-details-mode))
#+end_src

#+begin_src emacs-lisp
(defun t/dired-subtree-tab ()
  (interactive)
  (cond
   ((and (t/prefix-arg-universal?)
         (dired-subtree--is-expanded-p)) (t/dired-close-recursively))
   ((t/prefix-arg-universal?) (t/dired-open-recursively))
   (t (t/dired-subtree-toggle))))
(after! dired
  ;; prevent kill all dired buffers on q
  (map! :map dired-mode-map :ng "q" 't/volatile-kill-buffer)
  (map! :map dired-mode-map :ng "Q" 'evil-record-macro)
  (map!
   :map dired-mode-map
   "C-k" 'dired-kill-subdir
   "<tab>" 't/dired-subtree-tab
   "<backspace>" 'dired-kill-subdir
   "M-<down>" (cmd! (dired-find-alternate-file))
   "M-<up>" (cmd! (find-alternate-file ".."))))
#+end_src

*** Customize

Doom doesnt use the customize interface. It is useful nonetheless for experimenting with face colors etc

#+begin_src emacs-lisp
(set-popup-rule! "^*Customize" :ignore t)
#+end_src

Make ~s-s~ save in =customize=. Look up the function of a button using =describe-text-properties= on a button, like the "Apply and Save"

#+begin_src emacs-lisp
(map! :map custom-mode-map
      "s-s" 'Custom-save)
#+end_src

*** Directory sidebar

A diy directory sidebar using dired. Inspired the response in https://www.reddit.com/r/emacs/comments/nlefvx/weekly_tips_and_tricks/, adapted to doom.

#+begin_src emacs-lisp
(defun t-toggle-sidebar ()
  (interactive)
  ;; if buffer and window exists
  (let ((b (concat "*" "sidebar" ":" (+workspace-current-name) "*")))
    (if (and (get-buffer b)
             (get-buffer-window (get-buffer b)))
        (delete-window (get-buffer-window (get-buffer b)))

      ;; when no buffer exists, init it
      (when (not (get-buffer b))
        (with-current-buffer (dired-noselect (t/project-root))
          (rename-buffer b)))

      (pop-to-buffer b)
      (set-window-dedicated-p (selected-window) t)
      (set-window-parameter (selected-window) 'no-delete-other-windows t))))
#+end_src

~(popup +all)~ steals this, so move it to the left

#+begin_src emacs-lisp
(defun t/fit-sidebar (window)
  (interactive)
  (let* ((w (frame-width (window-frame)))
         (new-w (floor (/ w 4))))
    (evil-resize-window (- w new-w) t)))

(set-popup-rule! "^*sidebar*"
  :side 'left
  :ttl nil ;; don't kill it on C-g
  :quit 'current
  :slot  -1
  :width 't/fit-sidebar
  :parameters '((window-parameters . ((no-other-window . t)))))
#+end_src

*** Dired sidebar

#+begin_src emacs-lisp
(use-package! dired-sidebar
  :commands (dired-sidebar-toggle-sidebar
             dired-sidebar-point-at-file)
  :init
  (progn
    (setq dired-sidebar-window-fixed nil)
    (add-hook! 'dired-sidebar-mode-hook (defun t/dired-sidebar-hook ()
                                          (interactive)
                                          (visual-line-mode 0)
                                          (toggle-truncate-lines 1)
                                          (setq dired-sidebar-width (floor (* (frame-width (selected-frame))
                                                                              (/ (float 0.8) 3))))))))
#+end_src

*** Multiple cursors

#+begin_src emacs-lisp
(after! evil
  (defun t/mc-skip-prev ()
    (interactive)
    (evil-multiedit-toggle-or-restrict-region)
    (evil-multiedit-match-and-prev))

  (defun t/mc-skip-next ()
    (interactive)
    (evil-multiedit-toggle-or-restrict-region)
    (evil-multiedit-match-and-next)))
#+end_src

Make cursor follow matches so ~m-n~ or ~m-p~ can be used to skip matches easily, depending on what direction you are moving in.

#+begin_src emacs-lisp
(after! evil
  (setq evil-multiedit-follow-matches t)
  (map!
   :after evil
   :mode evil-multiedit-mode
   ;; for some reason m-j does not work, use m-n and m-p instead
   :n "M-n"   #'t/mc-skip-next
   :n "M-p"   #'t/mc-skip-prev

   ;; don't clash with ~evil-cp-delete-sexp~, require visual mode for multi edit
   :mode emacs-lisp-mode
   :v "M-d" 'evil-multiedit-match-symbol-and-next))
#+end_src

Restores a lost multiedit selection.

#+begin_src emacs-lisp
(map!
 :g "C-M-r" 'evil-multiedit-restore)
#+end_src

Multiedit calls iedit which is missing all-caps in emacs 29.

#+begin_src elisp
(when (version< "29.0" emacs-version)
  (defun all-caps (smtn)
    (upper smtn)))
#+end_src

*** Font

#+begin_quote
;; Doom exposes five (optional) variables for controlling fonts in Doom:
;;
;; - `doom-font' -- the primary font to use
;; - `doom-variable-pitch-font' -- a non-monospace font (where applicable)
;; - `doom-big-font' -- used for `doom-big-font-mode'; use this for
;;   presentations or streaming.
;; - `doom-unicode-font' -- for unicode glyphs
;; - `doom-serif-font' -- for the `fixed-pitch-serif' face
;;
;; See 'C-h v doom-font' for documentation and more examples of what they
;; accept. For example:
;;
#+end_quote

#+begin_quote
JetBrains Mono:pixelsize=16:foundry=JB:weight=medium:slant=normal:width=normal:spacing=100:scalable=true
#+end_quote

#+begin_src emacs-lisp
(defun t/font-spec (f &optional s weight)
  (font-spec :family f
             :size (or s 17)
             :weight (or weight 'normal)
             :slant 'normal
             :width 'normal))

(setq t-fonts '((:face "JetBrains Mono")))

(defun t/cycle-fonts (&optional font-spec)
  (interactive)
  (setq t-fonts (nconc (last t-fonts) (butlast t-fonts)))
  (let* ((spec (or font-spec (car t-fonts)))
         (f (plist-get spec :face))
         (s (plist-get spec :size))
         (w (plist-get spec :weight)))
    (message "Font: %s, size: %s, weight: %s" f s w)
    (setq doom-font (t/font-spec f s w)
          doom-variable-pitch-font (t/font-spec f s w)
          ;;doom-variable-pitch-font (t/font-spec "ETBembo" 20)
          doom-big-font (t/font-spec f 14)
          doom-font-increment 1)
    (doom/reload-font)
    f))

(t/cycle-fonts)
#+end_src

To list available fontsets, do

#+begin_src emacs-lisp
(comment
 (call-interactively 'describe-font))
#+end_src

#+begin_quote
;; If you or Emacs can't find your font, use 'M-x describe-font' to look them
;; up, `M-x eval-region' to execute elisp code, and 'M-x doom/reload-font' to
;; refresh your font settings. If Emacs still can't find your font, it likely
;; wasn't installed correctly. Font issues are rarely Doom issues!
#+end_quote

*** Errors

Navigate flycheck errors

#+begin_src emacs-lisp
(map!
 :leader
 (:prefix-map ("e" . "errors")
              (:when t
                :desc "Toggle flycheck"        "t" #'flycheck-mode
                :desc "List errors"            "l" #'flycheck-list-errors
                :desc "Jump to next error"     "n" #'flycheck-next-error
                :desc "Jump to previous error" "N" #'flycheck-previous-error)))
#+end_src

**** Eglot flycheck issue

https://github.com/doomemacs/doomemacs/issues/6466

#+begin_src emacs-lisp
(after! (eglot flycheck)
  (push 'eglot flycheck-checkers)
  (delq! 'eglot flycheck-checkers))
#+end_src

*** Projects

Ignore some extra folders from projectile

#+begin_src emacs-lisp
(after! projectile
  (add-to-list 'projectile-globally-ignored-directories "^build$")
  (add-to-list 'projectile-globally-ignored-directories "^target$")
  (add-to-list 'projectile-globally-ignored-directories "^\\.log$"))
#+end_src

*** Workspaces

#+begin_src emacs-lisp
(map!
 :leader "1" '+workspace/switch-to-0
 :leader "2" '+workspace/switch-to-1
 :leader "3" '+workspace/switch-to-2
 :leader "4" '+workspace/switch-to-3
 :leader "5" '+workspace/switch-to-4
 :leader "6" '+workspace/switch-to-5
 :leader "7" '+workspace/switch-to-6
 :leader "8" '+workspace/switch-to-7
 :leader "9" '+workspace/switch-to-8
 :leader "0" '+workspace/switch-to-final)
#+end_src

And fix ~super~ navigation across modes that steam ~SPC~.

#+begin_src emacs-lisp
(map!
 "s-1" '+workspace/switch-to-0
 "s-2" '+workspace/switch-to-1
 "s-3" '+workspace/switch-to-2
 "s-4" '+workspace/switch-to-3
 "s-5" '+workspace/switch-to-4
 "s-6" '+workspace/switch-to-5
 "s-7" '+workspace/switch-to-6
 "s-8" '+workspace/switch-to-7
 "s-9" '+workspace/switch-to-8
 "s-0" 'doom/reset-font-size)
#+end_src

Be explicit about when deleting workspaces

#+begin_src emacs-lisp
(after! (:and evil persp-mode)
  (define-key! persp-mode-map
    [remap delete-window] #'delete-window
    [remap evil-window-delete] #'delete-window))

(map!
 :map doom-leader-workspace-map
 :leader :desc "Other workspace" "TAB '" '+workspace/other
 :leader :desc "New workspace" "TAB w" '+workspace/new-named
 :leader :desc "Next workspace" "TAB n" '+workspace:switch-next
 :leader :desc "Previous workspace" "TAB p" '+workspace:switch-previous
 :leader :desc "Swap next" "TAB j" '+workspace/swap-right
 :leader :desc "Swap previous" "TAB k" '+workspace/swap-left)

(map!
 :desc "New workspace" "s-t" (cmd!
                              (let ((name (read-string "Workspace name: ")))
                                (condition-case nil (+workspace-new name) (error nil))
                                (+workspace-switch name)
                                (if (t/prefix-arg-universal?)
                                    (consult-recent-file)
                                  (call-interactively 'execute-extended-command))))
 :desc "Rename workspace" "s-r" '+workspace/rename)
#+end_src


*** Themes

There's a lot of good [[file:~/.emacs.d/.local/straight/repos/themes/themes/][doom themes]]. I [[file:~/.emacs.d/.local/straight/repos/themes/themes/doom-one-theme.el::bg `(,(doom-darken "#282c34" 0.5) "black" "black"][tuned doom-one a little]], darkening some of the colors even more. Its in [[file:themes/t-doom-one.el::(def-doom-theme t-doom-one][themes/t-doom-one-theme.el]].

#+begin_src emacs-lisp
(setq doom-theme 'doom-vibrant)
#+end_src

#+begin_src emacs-lisp
(defun t/cycle-theme ()
  (interactive)
  (cl-case doom-theme
    (doom-vibrant (load-theme 'doom-flatwhite))
    (doom-flatwhite (load-theme 't-doom-one))
    (t (load-theme 'doom-vibrant)))
  (message "Theme: %s" doom-theme))
(map! :leader "t t" #'t/cycle-theme)
#+end_src

**** Line numbers

#+begin_src emacs-lisp
;; This determines the style of line numbers in effect. If set to `nil', line
;; numbers are disabled. For relative line numbers, set this to `relative'.
(setq display-line-numbers-type nil)
(setq-hook! 'prog-mode-hook display-line-numbers-type 'relative)
#+end_src

Set across all real buffers

#+begin_src emacs-lisp
(comment
 (progn
   (t/in-all-buffers (lambda (b) (setq display-line-numbers 'relative)))
   (t/in-all-buffers (lambda (b) (setq display-line-numbers nil)))))
#+end_src

**** Rainbow mode

***** Rainbow mode in prog modes

#+begin_src emacs-lisp
(add-hook! '(prog-mode-hook css-mode-hook html-mode-hook) 'rainbow-mode)
#+end_src

***** Color parens uniformly

#+begin_src emacs-lisp
(custom-set-faces!
  '(show-paren-match :background nil :foreground "yellow" :weight bold)
  '(rainbow-delimiters-depth-1-face :foreground "DeepPink4" :overline nil :underline nil)
  '(rainbow-delimiters-depth-2-face :foreground "DeepPink3" :overline nil :underline nil)
  '(rainbow-delimiters-depth-3-face :foreground "DeepPink2" :overline nil :underline nil)
  '(rainbow-delimiters-depth-4-face :foreground "DeepPink1" :overline nil :underline nil)
  '(rainbow-delimiters-depth-5-face :foreground "maroon4" :overline nil :underline nil)
  '(rainbow-delimiters-depth-6-face :foreground "maroon3" :overline nil :underline nil)
  '(rainbow-delimiters-depth-7-face :foreground "maroon2" :overline nil :underline nil)
  '(rainbow-delimiters-depth-8-face :foreground "maroon1" :overline nil :underline nil)
  '(rainbow-delimiters-depth-9-face :foreground "VioletRed3" :overline nil :underline nil)
  '(rainbow-delimiters-depth-10-face :foreground "VioletRed2" :overline nil :underline nil)
  '(rainbow-delimiters-depth-11-face :foreground "VioletRed1" :overline nil :underline nil)
  '(rainbow-delimiters-unmatched-face :foreground "Red" :overline nil :underline nil))
#+end_src

*** Transparency

#+begin_src emacs-lisp
(let ((tr 98))
  (t/transparency tr)
  (advice-add #'doom/reload-theme :after (cmd! (t/transparency tr))))
#+end_src

*** Frame

Show the buffer and the file

#+begin_src emacs-lisp
(setq frame-title-format "%b (%f)")
#+end_src

*** Windows

#+begin_src emacs-lisp
(setq-default window-combination-resize t)
#+end_src

**** Resize using arrow keys

#+begin_src emacs-lisp
(map! :after evil
      :map evil-window-map
      "s" (t/micro-state nil
           "<left>" (cmd! (evil-resize-window (- (window-width) 8) t))
           "<right>" (cmd! (evil-resize-window (+ (window-width) 8) t))
           "<up>" (cmd! (evil-resize-window (+ (window-height) 4)))
           "<down>" (cmd! (evil-resize-window (- (window-height) 4)))))
#+end_src

*** Messages

#+begin_src emacs-lisp
(defvar +messages--auto-tail-enabled nil)

(defun +messages--auto-tail-a (&rest arg)
  "Make *Messages* buffer auto-scroll to the end after each message."
  (let* ((buf-name (buffer-name (messages-buffer)))
         ;; Create *Messages* buffer if it does not exist
         (buf (get-buffer-create buf-name)))
    ;; Activate this advice only if the point is _not_ in the *Messages* buffer
    ;; to begin with. This condition is required; otherwise you will not be
    ;; able to use `isearch' and other stuff within the *Messages* buffer as
    ;; the point will keep moving to the end of buffer :P
    (when (not (string= buf-name (buffer-name)))
      ;; Go to the end of buffer in all *Messages* buffer windows that are
      ;; *live* (`get-buffer-window-list' returns a list of only live windows).
      (dolist (win (get-buffer-window-list buf-name nil :all-frames))
        (with-selected-window win
          (goto-char (point-max))))
      ;; Go to the end of the *Messages* buffer even if it is not in one of
      ;; the live windows.
      (with-current-buffer buf
        (goto-char (point-max))))))

(defun +messages-auto-tail-toggle ()
  "Auto tail the '*Messages*' buffer."
  (interactive)
  (if +messages--auto-tail-enabled
      (progn
        (advice-remove 'message '+messages--auto-tail-a)
        (setq +messages--auto-tail-enabled nil)
        (message "+messages-auto-tail: Disabled."))
    (advice-add 'message :after '+messages--auto-tail-a)
    (setq +messages--auto-tail-enabled t)
    (message "+messages-auto-tail: Enabled.")))
#+end_src

*** Jump around

Some of these, like ~SPC j c~ works across windows when prefixed with ~C-u~.

#+begin_src emacs-lisp
(map!
 :leader
 (:prefix-map ("j" . "jump")
  (:when t
   :desc "Jump to window"      "w" #'ace-window
   :desc "Jump to line"        "l" #'avy-goto-line
   :desc "org: Jump to header" "h" #'avy-org-goto-heading-timer
   :desc "Jump to char"        "c" #'avy-goto-char-2
   :desc "Jump to char"        "C" #'avy-goto-char)))
#+end_src

**** Avy tweaks

#+begin_src emacs-lisp
(after! (avy evil-integration)
  (defun t/setup-avy (&optional frame)
    (interactive)
    (setq avy-keys '(?j ?f ?d ?k ?s ?a)
          avy-timeout-seconds 0.2
          ;;avy-all-windows 'all-frames
          avy-all-windows nil
          avy-case-fold-search nil
          avy-highlight-first t
          avy-style 'at-full
          avy-background t)

    (set-face-attribute 'avy-background-face nil :foreground "#3a485f")

    (let* ((f 'font-lock-keyword-face)
           (b nil)
           (c "#77e0c6")
           (c "#ffc777"))
      (set-face-attribute 'avy-lead-face   nil :background b :weight 'bold :foreground c)
      (set-face-attribute 'avy-lead-face-0 nil :background b :weight 'bold :foreground c)
      (set-face-attribute 'avy-lead-face-1 nil :background b :weight 'bold :foreground c)
      (set-face-attribute 'avy-lead-face-2 nil :background b :weight 'bold :foreground c)))
  (t/setup-avy)

  ;;Also after creating a new frame when emacs is in daemon mode
  (advice-add 'doom/reload-theme :after #'t/setup-avy))
#+end_src

*** Smartparens

#+begin_src emacs-lisp
(after! smartparens
  (sp-local-pair 'emacs-lisp-mode "`" "'" :when '(sp-in-docstring-p))
  (add-hook! (clojure-mode emacs-lisp-mode cider-repl-mode) :append #'smartparens-strict-mode)
  (sp-use-paredit-bindings))
#+end_src

And some extra for org mode

#+begin_src emacs-lisp
(after! smartparens
  (sp-with-modes 'org-mode
    (sp-local-pair "*" "*" :actions '(insert wrap) :unless '(sp-point-after-word-p sp-point-at-bol-p) :wrap "C-*" :skip-match 'sp--org-skip-asterisk)
    (sp-local-pair "~" "~" :unless '(sp-point-after-word-p) :post-handlers '(("[d1]" "SPC")))
    (sp-local-pair "<" ">" :unless '(sp-point-after-word-p) :post-handlers '(("[d1]" "SPC")))
    (sp-local-pair "=" "=" :unless '(sp-point-after-word-p) :post-handlers '(("[d1]" "SPC")))
    (sp-local-pair "«" "»")))
#+end_src

Smartparens-mode paredit bindings in org mode messes up ~M-up~ and ~M-down~, bring them back.

#+begin_src emacs-lisp
(add-hook! 'org-mode-hook
  (defun t/org-mode-hook ()
    (map!
     :map evil-motion-state-local-map
     "M-<up>"    'org-metaup
     "M-<down>"  'org-metadown
     "M-S-<right>" 'org-shiftmetaright
     "M-S-<left>" 'org-shiftmetaleft)))
#+end_src

Bring back ~C-k~ in the minibuffer. Overrides [[file:~/.emacs.d/modules/config/default/+evil-bindings.el::map! :map (evil-ex-completion-map evil-ex-search-keymap][+evil-bindings.el]].

#+begin_src emacs-lisp
(map! :map (evil-ex-completion-map evil-ex-search-keymap)
      :gi "C-k" #'kill-line)
(define-key!
  :keymaps +default-minibuffer-maps
  "C-k" #'kill-line)
#+end_src

**** Wrap around

Support wrapping sexps by holding super, both in normal mode and insert mode, from the front and the back of expressions.

#+begin_src emacs-lisp
(map! :map smartparens-mode-map
      ;; literally S-s-8 on a norwegian mac keyboard
      :n "s-(" (cmd! (evil-emacs-state nil)
                     (sp-wrap-with-pair "\(")
                     (evil-normal-state nil))
      :i "s-(" (cmd! (sp-wrap-with-pair "\("))

      ;; literally S-s-MetaRight-8 on my norwegian mac keyboard
      :n "s-{" (cmd! (evil-emacs-state nil)
                     (sp-wrap-with-pair "\{")
                     (evil-normal-state nil))
      :i "s-{" (cmd! (sp-wrap-with-pair "\{"))

      ;; literally S-MetaRight-8 on my norwegian mac keyboard
      :n "s-[" (cmd! (evil-emacs-state nil)
                     (sp-wrap-with-pair "\[")
                     (evil-normal-state nil))
      :i "s-[" (cmd! (sp-wrap-with-pair "\["))

      ;; literally S-s-9 on a norwegian mac keyboard
      :n "s-)" (cmd! (evil-emacs-state nil)
                     (backward-sexp)
                     (sp-wrap-with-pair "\(")
                     (forward-sexp)
                     (evil-normal-state nil))
      :i "s-)" (cmd! (backward-sexp)
                     (sp-wrap-with-pair "(")
                     (forward-sexp))

      ;; literally S-s-MetaRight-9 on my norwegian mac keyboard
      :n "s-}" (cmd! (evil-emacs-state nil)
                     (backward-sexp)
                     (sp-wrap-with-pair "\{")
                     (forward-sexp)
                     (evil-normal-state nil))
      :i "s-}" (cmd! (backward-sexp)
                     (sp-wrap-with-pair "\{")
                     (forward-sexp))

      ;; literally S-MetaRight-9 on my norwegian mac keyboard
      :n "s-]" (cmd! (evil-emacs-state nil)
                     (backward-sexp)
                     (sp-wrap-with-pair "\[")
                     (forward-sexp)
                     (evil-normal-state nil))
      :i "s-]" (cmd! (backward-sexp)
                     (sp-wrap-with-pair "\[")
                     (forward-sexp)))
#+end_src

**** Expand braces

[[file:~/.emacs.d/modules/config/default/config.el::dolist (brace '("(" "{" "\[")][Override this to always expand braces]].

#+begin_src emacs-lisp
(after! smartparens
  (sp-pair "{" nil :post-handlers '(("||\n[i]" "RET") ("| " " ")))
  (sp-pair "(" nil :post-handlers '(("||\n[i]" "RET") ("| " " ")))
  (sp-pair "[" nil :post-handlers '(("||\n[i]" "RET"))))
#+end_src

*** Distraction free / Zen
**** Writeroom, margins

A _really global_ global writeroom mode. The function is redefined such that if writeroom-major-modes is nil, writeroom-mode is activated in ALL buffers.

#+begin_src emacs-lisp
(setq writeroom-major-modes nil)
(after! writeroom-mode
  (defun turn-on-writeroom-mode ()
    (when (or (not writeroom-major-modes)
              (apply 'derived-mode-p writeroom-major-modes))
      (writeroom-mode 1))))
#+end_src

The [[https://docs.doomemacs.org/latest/modules/ui/zen/][doom default text scale of 2]] is a bit heavy

#+begin_src emacs-lisp
(setq +zen-text-scale 0)
#+end_src

Bring back text zoom in writeroom mode, moving away toggle mode-line, awkwardly bound to ~s-?~. Give it an even more awkward binding.

#+begin_src emacs-lisp
(map! :map writeroom-mode-map
      "s-?" (cmd! (text-scale-increase 1))
      "s-:" 'writeroom-toggle-mode-line)
#+end_src

And screens are big, so a bit more space for text is nice.

#+begin_src emacs-lisp
(after! writeroom
  (setq writeroom-width (/ (float 7) 9))
  (setq writeroom-width (/ (float 3) 5))
  (setq writeroom-width (/ (float 2) 3)))
(after! olivetti
  (add-hook! 'olivetti-mode-hook (defun t/olivetti-mode-hook ()
                                   (interactive)
                                   (setq ;; olivetti-minimum-body-width
                                    olivetti-body-width
                                    (floor (* (frame-width (selected-frame))
                                              ;; (/ (float 5) 7)
                                              (/ (float 2) 3)
                                              ))))))
#+end_src

Adjust margins equally across modes.

#+begin_src emacs-lisp
(map! :map evil-window-map
      "M" (t/micro-state
           nil
           "<left>" (cmd! (cond
                           ((and (boundp 'writeroom-mode) writeroom-mode) (writeroom-decrease-width))
                           ((and (boundp 'olivetti-mode) olivetti-mode) (olivetti-shrink))
                           (t (t/margins-global-decrease))))
           "<right>" (cmd! (cond
                            ((and (boundp 'writeroom-mode) writeroom-mode) (writeroom-increase-width))
                            ((and (boundp 'olivetti-mode) olivetti-mode) (olivetti-expand)
                            (t (t/margins-global-increase)))))))
#+end_src

*** Mode line

Show workspace in modeline, adjust bar width, moar iconz, truncate path.

#+begin_src emacs-lisp
(after! doom-modeline
  (setq doom-modeline-persp-name t
        doom-modeline-persp-icon nil
        doom-modeline-bar-width 4
        doom-modeline-mu4e t
        doom-modeline-major-mode-icon t
        doom-modeline-major-mode-color-icon t
        doom-modeline-buffer-file-name-style 'truncate-upto-root))
#+end_src

*** REPLs

#+begin_src emacs-lisp
(after! ielm
  (add-hook 'inferior-emacs-lisp-mode-hook 'evil-cleverparens-mode))
#+end_src

** Dotfiles

Highlight dotfiles that are sourced from the shell in ~sh-mode~ based on their file location.

#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist (cons (concat "^" (t/user-file "dotfiles") "/" "[^.]") 'sh-mode))
(add-to-list 'auto-mode-alist (cons (concat "^" (t/user-file "Projects/dotfiles") "/" "[^.]") 'sh-mode))
#+end_src

** Keybindings

- [[file:~/.emacs.d/modules/editor/evil/config.el::(map! :v "@" #'+evil:apply-macro][Doom editor keybindings]]
- [[file:~/.emacs.d/modules/config/default/+evil-bindings.el::map! :map (evil-ex-completion-map evil-ex-search-keymap][+evil-bindings.el]]
- [[file:~/.emacs.d/modules/editor/evil/config.el:::n "g=" #'evil-numbers/inc-at-pt][evil commands]]

#+begin_src emacs-lisp
(map! :map markdown-mode-map
      "M-p" 'backward-paragraph
      "M-n" 'forward-paragraph)
#+end_src

#+begin_src emacs-lisp
(map!
 :n "s-0" nil
 "s-0" #'doom/reset-font-size
 "s-+" #'doom/increase-font-size
 "s--" #'doom/decrease-font-size

 "s-?" (cmd! (text-scale-increase 1))
 "s-_" (cmd! (text-scale-decrease 1))
 "s-=" (cmd! (text-scale-set 0))

 "s-d" #'t/split-window-right-and-move-there-dammit
 "s-D" #'t/split-window-below-and-move-there-dammit

 "s-M-<up>" 'evil-window-up
 "s-M-<right>" 'evil-window-right
 "s-M-<down>" 'evil-window-down
 "s-M-<left>" 'evil-window-left

 "C-s-<left>" 't/decrease-frame-width
 "C-s-<right>" 't/increase-frame-width
 "C-s-<down>" 't/increase-frame-height
 "C-s-<up>" 't/decrease-frame-height

 "M-n" 'forward-paragraph
 "M-p" 'backward-paragraph

 ;; g = global
 :g "M-y" 'consult-yank-from-kill-ring

 ;; i = insert
 :i "C-d" #'delete-char
 :i "C-k" #'evil-delete-line
 :i "C-p" #'previous-line
 :i "C-n" #'next-line

 "s-k" 'previous-buffer
 "s-j" 'next-buffer
 "s->" 'next-multiframe-window
 "s-<" 'previous-multiframe-window
 "s-<left>" 't/smart-beginning-of-line
 "s-<right>" 'end-of-line

 "C-." 't/hippie-expand-no-case-fold
 "C-a" 't/smart-beginning-of-line

 ;; m = motion
 :m "C-e" 'end-of-line

 "s-q" 'save-buffers-kill-emacs
 "s-n" 'make-frame
 "s-s" 'save-buffer
 "s-w" #'t/delete-frame-or-hide-last-remaining-frame

 ;; op -- :leader :desc "Toggle treemacs" "f L" #'+treemacs/toggle
 :leader :desc "Open folder" "p o" #'t/open-in-desktop

 ;; :leader :desc "Toggle directory sidebar" "f l" #'t-toggle-sidebar
 :leader :desc "Toggle directory sidebar" "f l" #'dired-sidebar-toggle-sidebar
 :leader :desc "Toggle directory sidebar, follow" "f L" (cmd! (dired-sidebar-point-at-file buffer-file-name (doom-project-root)))

 :leader :desc "Browse"          "o e" #'eww
 :leader :desc "Www"             "o w" #'eww

 :leader :desc "Rss"               "o S" #'=rss
 :leader :desc "Twitter"           "o W" #'=twitter
 :leader :desc "Calendar"          "o c" #'calendar
 :leader :desc "Mail"              "o m" (cmd! (t/gpg) (=mu4e))
 :leader :desc "Mastodon"          "o M" (cmd! (condition-case nil (+workspace-new "*mastodon*") (error nil))
                                               (+workspace-switch "*mastodon*")
                                               (mastodon))
 :leader :desc "Show home"         "o h" #'(lambda () (interactive) (find-file (t/user-dropbox-folder "org/home.org.gpg")))
 :leader :desc "Show saga"         "o s" #'(lambda () (interactive) (find-file (t/user-dropbox-folder "org/saga.org.gpg")))
 :leader :desc "Open Intellij"     "o i" #'t/open-in-intellij
 :leader :desc "Browse at point"   "o b" #'t/browse-url-at-point
 :leader :desc "Browse chrome url" "o B" #'t/browse-chrome-url-in-eww

 :leader :desc "Eval and replace" "m e R" #'t/eval-and-replace

 :leader :desc "Search the web" "s w" #'consult-web-search

 :leader :desc "Toggle blamer"         "t b" #'blamer-mode
 :leader :desc "Toggle cursor"         "t c" #'t/toggle-window-cursor
 :leader :desc "Fill column indicator" "t C" #'display-fill-column-indicator-mode
 :leader :desc "Toggle Big mode"       "t B" #'doom-big-font-mode
 :leader :desc "Toggle dedication"     "t d" #'t/toggle-dedicated-window
 :leader :desc "Debug on error"        "t D" #'toggle-debug-on-error
 :leader :desc "Cycle fonts"           "t f" #'t/cycle-fonts
 :leader :desc "Toggle focus mode"     "t F" #'focus-mode
 :leader :desc "Toggle highlight line" "t h" #'hl-line-mode
 :leader :desc "Toggle hackernews"     "t H" (cmd! (condition-case nil (+workspace-new "*hn*") (error nil))
                                                   (+workspace-switch "*hn*")
                                                   (hnreader-news))
 :leader :desc "Toggle gnus"           "t g" (cmd! (condition-case nil (+workspace-new "*gnus*") (error nil))
                                                   (+workspace-switch "*gnus*")
                                                   (gnus))
 :leader :desc "Toggle line wrapping"  "t v" #'visual-line-mode
 :leader :desc "Toggle truncate"       "t u" #'toggle-truncate-lines
 :leader :desc "Toggle margins"        "t M" #'t/margins-global
 :leader :desc "Toggle olivetti"       "t o" #'olivetti-mode
 :leader :desc "Toggle transparency"   "t T" #'t/transparency
 :leader :desc "Reading"               "t r" #'t/start-spray-micro-state
 :leader :desc "Toggle reddit"         "t R" (cmd! (condition-case nil (+workspace-new "*reddit*") (error nil))
                                                   (+workspace-switch "*reddit*")
                                                   (reddigg-view-frontpage))
 :leader :desc "Toggle writeroom"      "t w" #'global-writeroom-mode
 :leader :desc "Show whitespace"       "t W" #'whitespace-mode
 :leader :desc "Toggle zen"            "t z" #'global-writeroom-mode

 :leader :desc "Flip frame"                   "w f" #'rotate-frame
 :leader :desc "Close window unconditionally" "w d" #'t/delete-window-or-frame-or-hide
 :leader :desc "Winner redo"                  "w R" #'winner-redo
 :leader :desc "Rotate frame"                 "w r" (cmd!
                                                     (if (t/prefix-arg-universal?)
                                                         (rotate-frame-anticlockwise)
                                                       (rotate-frame-clockwise)))

 :leader :desc "Projectile dired"    "p d" #'t/projectile-dired
 :leader :desc "Projectile git"      "p g" #'t/projectile-magit-status
 :leader :desc "Projectile pulls"    "p P" #'t/projectile-visit-git-link-pulls
 :leader :desc "Scratch buffer" "b s" #'doom/open-scratch-buffer

 :leader :desc "Previous occurrence" "h p" #'highlight-symbol-prev
 :leader :desc "Previous occurrence" "h N" #'highlight-symbol-prev
 :leader :desc "Next occurrence"     "h n" #'highlight-symbol-next)
 #+end_src

Hide the last frame on os x instead of nuking it

#+begin_src emacs-lisp
(map! :leader "q f" 't/delete-frame-or-hide-last-remaining-frame)
#+end_src

That's irritating. Prevent drag-stuff-mode from messing things up

#+begin_src emacs-lisp
(map!
 :after drag-stuff-mode
 :map drag-stuff-mode-map
 "<M-up>"    #'drag-stuff-up ;; messes up org mode
 "<M-down>"  #'drag-stuff-down ;; messes up org mode
 ;; :ni "<M-left>"  #'evil-backward-word-begin
 ;; :ni "<M-right>" #'evil-forward-word-begin
 )
#+end_src

Popup bindings on a norwegian keyboard

#+begin_src emacs-lisp
(map! :g "C-*"   #'+popup/raise
      :g "C-x p" #'+popup/other
      :g "C-'"   #'+popup/toggle
      :map org-mode-map
      :g "C-*"   #'+popup/raise
      :g "C-'"   #'+popup/toggle)
#+end_src

** Gnus

#+begin_src emacs-lisp
(set-popup-rule! "^*Summary" :side 'bottom :size 0.5)
(set-popup-rule! "^*Article" :side 'bottom :size 0.5)
(setq gnus-select-method '(nntp "news.gmane.io")) ; A A
#+end_src

** Help

One help shortcut, everywhere.

#+begin_src emacs-lisp
(map! :leader :n "h h" #'helpful-at-point)
#+end_src

Keep them on the side for some more room.

#+begin_src emacs-lisp
(set-popup-rule! "^*info" :side 'right :width 82)
(set-popup-rule! "^*help" :side 'right :width 82)
#+end_src

*** Motions

Make helpful buffers more navigable by removing doom popup's /dedication/. This makes ~q~ fall back to the previous help buffer after a help link click that made you navigate to the next help topic.

#+begin_src emacs-lisp
(advice-add
 #'push-button
 :after (defun t/keep-help-buffers-around ()
         (set-window-dedicated-p (selected-window) nil)
         (set-window-parameter (selected-window) 'no-delete-other-windows nil)))
#+end_src

*** Info mode

#+begin_src emacs-lisp
(after! info
  (map!
   :map Info-mode-map
   "M-n" #'forward-paragraph
   "M-p" #'backward-paragraph))
#+end_src

**** Motions

Motion keys for info mode.

#+BEGIN_SRC emacs-lisp :results silent
(after! evil
  (after! info
         (evil-define-key 'normal Info-mode-map (kbd "H") 'Info-history-back)
         (evil-define-key 'normal Info-mode-map (kbd "L") 'Info-history-forward)
         (unbind-key (kbd "h") 'Info-mode-map)
         (unbind-key (kbd "l") 'Info-mode-map)))
#+END_SRC

** Org

*** Org settings

#+begin_src emacs-lisp
(after! org

  (add-hook! 'org-mode-hook 'evil-cleverparens-mode)

  (defun t/open-prev-heading ()
    (interactive)
    (let ((was-narrowed (buffer-narrowed-p)))
      (when was-narrowed (widen))
      (when (org-at-heading-p)
        (outline-hide-sublevels (org-outline-level)))
      (org-previous-visible-heading 1)
      (outline-show-subtree)
      (when was-narrowed (org-narrow-to-subtree))
      (recenter-top-bottom 0)))

  (defun t/open-next-heading ()
    (interactive)
    (let ((was-narrowed (buffer-narrowed-p)))
      (when was-narrowed (widen))
      (when (org-at-heading-p)
        (outline-hide-sublevels (org-outline-level)))
      (org-next-visible-heading 1)
      (outline-show-subtree)
      (when was-narrowed (org-narrow-to-subtree))
      (recenter-top-bottom 0)))

  ;; like in normal org, not like in doom
  (map! :after evil-org
        :map evil-org-mode-map
        :ni "C-<return>" #'org-insert-heading-respect-content

        ;; bring back deleting characters from insert in org mode
        :i "C-d" nil

        :map org-mode-map
        :ni "C-c C-p" #'t/open-prev-heading
        :ni "C-c C-n" #'t/open-next-heading)

  ;; Include gpg files in org agenda
  (unless (string-match-p "\\.gpg" org-agenda-file-regexp)
    (setq org-agenda-file-regexp
          (replace-regexp-in-string "\\\\\\.org" "\\\\.org\\\\(\\\\.gpg\\\\)?"
                                    org-agenda-file-regexp)))

  (defun t/org-capture-chrome-link-template (&optional &rest args)
    "Capture current frontmost tab url from chrome."
    (concat "* TODO %? :url:\n\n" (t/grab-chrome-url)))

  (defun t/org-capture-link-template (&optional &rest args)
    "Capture url."
    (concat "* TODO %? %^G\n\nLink:\n - "
            (cond
             ((equal major-mode 'mu4e-view-mode) (concat "mu4e:msgid:" (plist-get (mu4e-message-at-point) :message-id)))
             ((equal major-mode 'mu4e-headers-mode) (concat "mu4e:msgid:" (plist-get (mu4e-message-at-point) :message-id)))
             ((equal major-mode 'elfeed-show-mode) (elfeed-entry-link elfeed-show-entry))
             ((equal major-mode 'elfeed-search-mode) (s-join "\n - " (cl-loop for feed in (elfeed-search-selected)
                                                                              collect (elfeed-entry-link feed))))
             (t (get-text-property (point) 'shr-url)))))

  (setq org-tags-column -60
        org-support-shift-select t   ; shift can be used to mark multiple lines
        org-special-ctrl-k t         ; don't clear tags, etc
        org-special-ctrl-a/e t       ; don't move past ellipsis on c-e
        org-agenda-skip-scheduled-if-done t
        org-default-notes-file (expand-file-name "~/Dropbox/org/home.org.gpg")
        org-log-done 'time           ; log when todos are completed
        org-log-redeadline 'time     ; log when deadline changes
        org-log-reschedule 'time     ; log when schedule changes
        org-reverse-note-order t     ; newest notes first
        org-return-follows-link t    ; go to http links in browser
        org-todo-keywords '((sequence "TODO(t)" "STARTED(s)" "|" "DONE(d)" "CANCELLED(c)"))))
#+end_src

*** Focus subtree after jumping

Zoom to the previewed subtree when jumping between with =consult-org-heading=.

#+begin_src emacs-lisp
(add-hook 'consult-after-jump-hook
          (defun t/after-consult-jump ()
            ;; org
            (when (eq major-mode 'org-mode)
              (when (org-at-heading-p)
                (outline-hide-sublevels (org-outline-level)))
              (org-show-subtree))
            ;; always
            (recenter-top-bottom 0)))
#+end_src

*** Async source code blocks

Make it possible to use the header argument ~:async true~ for async execution of begin_src code blocks.

#+begin_src emacs-lisp
(after! org
  (require 'ob-async))
#+end_src

*** Agenda

**** Custom commands

Org agenda customizations

#+begin_src emacs-lisp
(defun t/org-agenda-todo-type (name)
  `((org-agenda-remove-tags t)
    (org-agenda-sorting-strategy '(tag-up priority-down))
    (org-agenda-todo-keyword-format "")
    (org-agenda-overriding-header ,name)))

(defun t/org-agenda-day (tags)
  (list tags `((org-agenda-span 'day)
               (org-agenda-tag-filter-preset ,tags))))


(defun t/org-agenda-pri (header tags)
  (list tags `((org-agenda-overriding-header ,header)
               (org-agenda-skip-function '(or (org-agenda-skip-entry-if 'todo 'done)
                                              (and (org-agenda-skip-entry-if 'notregexp "\\[#A\\]")
                                                   (org-agenda-skip-entry-if 'notregexp "\\[#B\\]")
                                                   (org-agenda-skip-entry-if 'notregexp "\\[#C\\]")))))))

(defun t/org-agenda-not-pri (header tags skip)
  (list tags `((org-agenda-overriding-header ,header)
               (org-agenda-skip-function '(or (org-agenda-skip-entry-if 'regexp "\\[#A\\]")
                                              (org-agenda-skip-entry-if 'regexp "\\[#B\\]")
                                              (org-agenda-skip-entry-if 'regexp "\\[#C\\]")
                                              (org-agenda-skip-if nil (quote ,skip)))))))

(defun t/org-agenda-todos (header tags)
  (t/org-agenda-not-pri header tags '(scheduled deadline)))

(defun t/org-agenda-todos-scheduled (header tags)
  (t/org-agenda-not-pri header tags '(notscheduled deadline)))

(defun t/org-day-summary (&rest tags)
  `((agenda    ,@(t/org-agenda-day (string-join tags "|")))
    (tags      ,@(t/org-agenda-pri "Pri" (string-join tags "|")))
    (tags-todo ,@(t/org-agenda-todos "Todo" (string-join tags "|")))
    (tags-todo ,@(t/org-agenda-todos-scheduled "Scheduled todo" (string-join tags "|")))))

(defun t/org-agenda-read ()
  `(tags-todo "book|read|twitter|pocket" ((org-agenda-overriding-header "Read"))))

(defun t/org-done-today (tag)
  `(tags ,(format "%s+CLOSED>=\"<today>\"" tag) ((org-agenda-overriding-header "\nCompleted today\n"))))

;; and some custom agenda shortcuts using them
(setq org-agenda-custom-commands
      `(("n" "Agenda and all TODOs" ((agenda "") (alltodo "")))
        ("m" tags-todo "serie|film")
        ("e" tags-todo "emacs")
        ("r" ,@(t/org-agenda-read))
        ("v" tags-todo "video")
        ("T" alltodo)
        ("C" todo "DONE" ,(t/org-agenda-todo-type "DONE"))
        ("t" todo "TODO" ,(t/org-agenda-todo-type "TODO"))
        ("b" todo "STARTED" ,(t/org-agenda-todo-type "STARTED"))
        ("c" todo "CANCELLED" ,(t/org-agenda-todo-type "CANCELLED"))
        ("w" "work" ,(append (t/org-day-summary "+bekk" "+saga")
                             `((tags "+someday+saga")
                               (tags "+someday+bekk")
                               ,(t/org-done-today "+work"))))
        ("h" "home" ,(append (t/org-day-summary "+home-emacs-someday")
                             `(,(t/org-agenda-read)
                               (tags-todo "+someday-work" ((org-agenda-overriding-header "Someday")))
                               ,(t/org-done-today "+home"))))))
#+end_src

**** Clock

#+begin_src emacs-lisp
(defun t/org-clock-start () (interactive) (org-todo "STARTED"))
(defun t/org-clock-stop () (interactive) (org-todo))
(advice-remove 'org-clock-in 't/org-clock-start)
(advice-remove 'org-clock-out 't/org-clock-stop)
(advice-add 'org-clock-in :after 't/org-clock-start)
(advice-add 'org-clock-out :after 't/org-clock-stop)
#+end_src

*** Keybindings

Extensions of some of the [[file:~/.emacs.d/modules/lang/org/config.el::(map! :map org-mode-map][Doom org mode map bindings]].

Heading and item bindings

- ~C-ret~ :: new below, insert mode, same level
- ~C-S-ret~ :: new above, insert mode, same level
- ~M-ret~ :: new heading, normal mode, same level
- ~M-S-ret~ :: todo below, normal mode, same level
- ~C-M-ret~ :: heading below, normal mode, level down
- ~SPC-m-h~ :: heading from text
- ~SPC-m-i~ :: item from text

~SPC g a~ seems more reasonable than ~SPC g G~. [[https://discourse.doomemacs.org/t/what-are-leader-and-localleader-keys/153][Localleader in doom]] is bound to ~SPC m~.

#+begin_src emacs-lisp
(map! :map org-mode-map
      :localleader "g a" #'consult-org-agenda)
#+end_src

Widen

#+begin_src emacs-lisp
(map!
 :map org-mode-map
 :localleader :desc "Widen" "s w" 'widen
 :localleader :desc "Narrow to subtree" "s n" 'org-narrow-to-subtree)
#+end_src

Save from agenda

#+begin_src emacs-lisp
(map! :after org-agenda
      :map evil-org-agenda-mode-map
      :m "H" #'org-agenda-earlier
      :m "L" #'org-agenda-later
      "s-s" #'org-save-all-org-buffers)
#+end_src

*** Colors

#+begin_src emacs-lisp
(after! org
  (set-face-attribute 'org-todo nil :foreground "#94fFe4" :weight 'bold))
#+end_src

*** Make links appear

#+begin_src emacs-lisp
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers t
        org-appear-autolinks nil)
  ;; for proper first-time setup, `org-appear--set-elements'
  ;; needs to be run after other hooks have acted.
  (run-at-time nil nil #'org-appear--set-elements))
#+end_src

*** Org links

Make org handle links load links that start with

- ~eww:~
- ~eshell~
- ~man:~
- ~vterm:~

#+begin_src emacs-lisp
(add-hook! 'org-mode-hook
  (defun t/load-org-links ()
    (interactive)
    (require 'ol)
    (require 'ol-eshell)
    (require 'ol-man)
    (require 'ol-eww)
    (defun t/org-vterm-open (url _)
      "Open URL with vterm in the current buffer."
      (let ((current-prefix-arg 1))
        (call-interactively '+vterm/toggle)
        (term-send-raw-string (concat url "\C-m"))))
    (org-link-set-parameters "vterm" :follow 't/org-vterm-open)))
#+end_src

*** Refile

Save org mode buffers after refile.

#+begin_src emacs-lisp
(defadvice org-refile (after t/after-org-refile activate)
  (org-save-all-org-buffers))
#+end_src

*** Tables

#+begin_src emacs-lisp
(after! evil
  (when (boundp 'org-evil-table-mode-map)
    (map!
     :map org-evil-table-mode-map
     "M-S-<left>" 'org-table-delete-column
     "M-S-<right>" 'org-table-insert-column)))
#+end_src

*** Hugo capture template

#+begin_src emacs-lisp
(after! org
  (with-eval-after-load 'org-capture
    (defun org-hugo-new-subtree-post-capture-template ()
      "Returns `org-capture' template string for new Hugo post.
See `org-capture-templates' for more information.
https://ox-hugo.scripter.co/doc/org-capture-setup/"
      (let* ((title (read-from-minibuffer "Post Title: "))
             (fname (org-hugo-slug title)))
        (mapconcat #'identity
                   `(,(concat "* TODO " title)
                     ":PROPERTIES:"
                     ,(concat ":EXPORT_FILE_NAME: " fname)
                     ":END:" "%?\n")
                   "\n")))))
#+end_src

*** Structure templates

Remove the ~s~ mapping for source code blocks.

#+begin_src emacs-lisp
(after! org
  (setq org-structure-template-alist (remove '("s" "src") org-structure-template-alist)))
#+end_src

Replace it with ~ss~ (its faster than the default ~s ~) so we can add some more along side it.

#+begin_src emacs-lisp
(after! org
  (add-to-list 'org-structure-template-alist (cons "ss" "src"))
  (add-to-list 'org-structure-template-alist (cons "se" "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist (cons "sp" "src python"))
  (add-to-list 'org-structure-template-alist (cons "sj" "src javascript"))
  (add-to-list 'org-structure-template-alist (cons "sh" "src sh"))
  (add-to-list 'org-structure-template-alist (cons "aI" "ai :image :size 512x512"))
  (add-to-list 'org-structure-template-alist (cons "ai" "ai")))
#+end_src

If you need to remove one, do this

#+begin_src emacs-lisp
(comment
 (setq org-structure-template-alist (remove '("sh" . "src shell") org-structure-template-alist)))
#+end_src

Don't popupize the org code block editor with doom's popup framework, so it opens split wherever it fits like it is by default.

#+begin_src emacs-lisp
(after! org
  (set-popup-rule! "^*Org Src" :ignore t))
#+end_src

*** Capture templates

#+begin_src emacs-lisp
(after! org

  (setq org-capture-templates
        `(("t" "Task" entry (file+olp org-default-notes-file "tasks") "* TODO %? \n\n%i\n\n" :prepend t :empty-lines-after 1)
          ("s" "Saga" entry (file+olp ,(t/user-dropbox-folder "org/saga.org.gpg") "Tasks") "* TODO %? \n\n%i" :prepend t :empty-lines-after 1)
          ("b" "bekk" entry (file+olp ,(t/user-dropbox-folder "org/bekk.org.gpg") "Tasks") "* TODO %? \n\n%i" :prepend t :empty-lines-after 1)
          ("f" "File" entry (file+olp org-default-notes-file "Tasks") "* TODO %? %^G\n\n%i%a\n\n" :prepend t :empty-lines-after 1)
          ("l" "Link (eww, mu4e, etc)" entry (file+olp org-default-notes-file "Tasks") (function t/org-capture-link-template) :prepend t :empty-lines-after 1)
          ("c" "Chrome location" entry (file+olp org-default-notes-file "Tasks") (function t/org-capture-chrome-link-template) :prepend t :empty-lines-after 1)
          ("p" "Post" entry (file+olp "~/Code/posts/content-org/blog.org" "Drafts") (function org-hugo-new-subtree-post-capture-template))
          ;; ("T" "Personal todo" entry (file+headline +org-capture-todo-file "Inbox") "* %?\n %i\n %a" :prepend t)
          ;; ("j" "Journal" entry (file+olp+datetree +org-capture-journal-file) "* %U %?\n %i\n %a" :prepend t)
          ;; ("n" "Personal notes" entry (file+headline +org-capture-notes-file "Inbox") "* %u %?\n %i\n %a" :prepend t)
          ;; ("p" "Templates for projects") ("pt" "Project-local todo" entry (file+headline +org-capture-project-todo-file "Inbox") "* TODO %?\n %i\n %a" :prepend t)
          ;; ("pn" "Project-local notes" entry (file+headline +org-capture-project-notes-file "Inbox") "* %U %?\n %i\n %a" :prepend t)
          ;; ("pc" "Project-local changelog" entry (file+headline +org-capture-project-changelog-file "Unreleased") "* %U %?\n %i\n %a" :prepend t)
          ;; ("o" "Centralized templates for projects")
          ;; ("ot" "Project todo" entry #'+org-capture-central-project-todo-file "* TODO %?\n %i\n %a" :heading "Tasks" :prepend nil)
          ;; ("on" "Project notes" entry #'+org-capture-central-project-notes-file "* %U %?\n %i\n %a" :heading "Notes" :prepend t)
          ;; ("oc" "Project changelog" entry #'+org-capture-central-project-changelog-file "* %U %?\n %i\n %a" :heading "Changelog" :prepend t)
          )))
#+end_src

*** Text Objects

**** evil-org-outer-subtree

#+BEGIN_SRC emacs-lisp :results silent
(after! evil
  (evil-define-text-object evil-org-outer-subtree (count &optional beg end type)
    "An Org subtree.  Uses code from `org-mark-subtree`"
    :type line
    (save-excursion
      ;; get to the top of the tree
      (org-with-limited-levels
       (cond ((org-at-heading-p) (beginning-of-line))
             ((org-before-first-heading-p) (user-error "Not in a subtree"))
             (t (outline-previous-visible-heading 1))))

      (cl-decf count)
      (when count (while (and (> count 0) (org-up-heading-safe)) (cl-decf count)))

      ;; extract the beginning and end of the tree
      (let ((element (org-element-at-point)))
        (list (org-element-property :end element)
              (org-element-property :begin element))))))
#+END_SRC

**** evil-org-inner-subtre

#+BEGIN_SRC emacs-lisp :results silent
(after! evil
  (evil-define-text-object evil-org-inner-subtree (count &optional beg end type)
    "An Org subtree, minus its header and concluding line break.  Uses code from `org-mark-subtree`"
    :type line
    (save-excursion
      ;; get to the top of the tree
      (org-with-limited-levels
       (cond ((org-at-heading-p) (beginning-of-line))
             ((org-before-first-heading-p) (user-error "Not in a subtree"))
             (t (outline-previous-visible-heading 1))))

      (cl-decf count)
      (when count (while (and (> count 0) (org-up-heading-safe)) (cl-decf count)))

      ;; extract the beginning and end of the tree
      (let* ((element (org-element-at-point))
             (begin (save-excursion
                      (goto-char (org-element-property :begin element))
                      (next-line)
                      (point)))
             (end (save-excursion
                    (goto-char (org-element-property :end element))
                    (backward-char 1)
                    (point))))
        (list end begin)))))
#+END_SRC

**** evil-org-outer-item

#+BEGIN_SRC emacs-lisp :results silent
(after! evil
  (evil-define-text-object evil-org-outer-item (count &optional beg end type)
    :type line
    (let* ((struct (org-list-struct))
           (begin (org-list-get-item-begin))
           (end (org-list-get-item-end (point-at-bol) struct)))
      (if (or (not begin) (not end))
          nil
        (list begin end)))))
#+END_SRC

**** evil-org-inner-item

#+BEGIN_SRC emacs-lisp :results silent
(after! evil
  (evil-define-text-object evil-org-inner-item (count &optional beg end type)
    (let* ((struct (org-list-struct))
           (begin (progn (goto-char (org-list-get-item-begin))
                         (forward-char 2)
                         (point)))
           (end (org-list-get-item-end-before-blank (point-at-bol) struct)))
      (if (or (not begin) (not end))
          nil
        (list begin end)))))
#+END_SRC

**** Bind them

#+BEGIN_SRC emacs-lisp :results silent
(define-key evil-outer-text-objects-map "h" 'evil-org-outer-subtree)
(define-key evil-inner-text-objects-map "h" 'evil-org-inner-subtree)
(define-key evil-outer-text-objects-map "*" 'evil-org-outer-subtree)
(define-key evil-inner-text-objects-map "*" 'evil-org-inner-subtree)
(define-key evil-outer-text-objects-map "i" 'evil-org-outer-item)
(define-key evil-inner-text-objects-map "i" 'evil-org-inner-item)
(define-key evil-outer-text-objects-map "-" 'evil-org-outer-item)
(define-key evil-inner-text-objects-map "-" 'evil-org-inner-item)
#+END_SRC

*** Pomodoro                                       :emacs:

#+begin_src emacs-lisp
(after! org
  (setq org-pomodoro-format "%s"))
#+end_src

*** ChatGPT and DALL-E in org mode


#+begin_src emacs-lisp
(use-package! org-ai
  :hook (org-mode . org-ai-mode))
#+end_src

** Reading
*** Fast

I never really got into this.

#+begin_src emacs-lisp
(defun t/spray-micro-state (&optional after)
  (t/micro-state-in-mode
   'spray-mode
   after
   "s" 'spray-slower
   "f" 'spray-faster
   "SPC" 'spray-start/stop
   "b" 'spray-backward-word
   "w" 'spray-forward-word
   "<left>" 'spray-backward-word
   "<right>" 'spray-forward-word))

(defun t/start-spray-micro-state (&optional on-exit)
  (interactive)
  (let ((map (make-sparse-keymap)))
    (bind-key "n" (lambda ()
                    (interactive)
                    (condition-case nil
                        (scroll-up-command)
                      (error
                       (cond
                        ((eq major-mode 'elfeed-show-mode) (elfeed-show-next))
                        ((eq major-mode 'mu4e-view-mode) (mu4e-view-headers-next-unread)))))) map)
    (bind-key "p" (lambda ()
                    (interactive)
                    (condition-case nil
                        (scroll-down-command)
                      (error
                       (cond
                        ((eq major-mode 'elfeed-show-mode) (elfeed-show-prev))
                        ((eq major-mode 'mu4e-view-mode) (mu4e-view-headers-prev-unread)))))) map)
    (bind-key "s" (cmd!
                   (when (eq major-mode 'elfeed-show-mode)
                     (let ((shr-inhibit-images t)) (elfeed-show-refresh)))
                   (funcall (t/spray-micro-state))) map)
    (bind-key "S" (cmd!
                   (mark-whole-buffer)
                   (call-interactively 'chatgpt-summarize-region-and-replace)
                   (deactivate-mark)
                   (beginning-of-buffer)) map)
    (bind-key "o" (cmd!
                   (call-interactively 'olivetti-mode)) map)
    (set-temporary-overlay-map map t on-exit))
  (message "(n)ext page, (p)rev page, (s)pray mode, (S)ummarize with chatgpt, (o)livetti"))

(map! :leader :desc "Toggle spray" "t s" (t/spray-micro-state))

(after! spray
  (setq spray-wpm 720
        spray-height nil)
  (add-hook 'spray-mode-hook #'t/spray-mode-hook)
  (defun t/spray-mode-hook ()
    (setq-local spray-margin-top (truncate (/ (window-height) 2.5)))
    (setq-local spray-margin-left (truncate (/ (window-width) 3.5)))
    (set-face-foreground 'spray-accent-face
                         (face-foreground 'font-lock-keyword-face))))
#+end_src

*** Eww

An elisp web browser.

**** Lookup

Make ~SPC s o~ open in eww first, then use ~&~ to go to the default browser if needed.

#+begin_src emacs-lisp
(setq +lookup-open-url-fn #'eww)
#+end_src

**** Popup size

#+begin_src emacs-lisp
(after! evil
  ;; the original way
  ;;(setf (alist-get 'size (display-buffer-assq-regexp "*eww*" display-buffer-alist nil)) 0.8)
  ;; the doom way
  (set-popup-rule! "^\\*eww*" :side 'bottom :size 0.8))
#+end_src

**** Readability

Enter readable mode automatically, normally available from pressing ~R~ in eww mode.

#+begin_src emacs-lisp
(add-hook 'eww-after-render-hook 'eww-readable)
#+end_src

Eww functions that directly enter the eww readability mode after loading a given url

#+BEGIN_SRC emacs-lisp
(defun t/eww-readable-after-render (status url buffer fn)
  (eww-render status url nil buffer)
  (switch-to-buffer buffer)
  (eww-readable)
  (let ((content (buffer-substring-no-properties (point-min) (point-max))))
    (read-only-mode 0)
    (erase-buffer)
    (insert content)
    (beginning-of-buffer)
    (toggle-truncate-lines -1)
    (when fn (funcall fn))))

(defun t/eww-readable (url &optional fn)
  (interactive "sEnter URL: ")
  (let ((buffer (get-buffer-create "*eww*")))
    (with-current-buffer buffer
      (autoload 'eww-setup-buffer "eww")
      (eww-setup-buffer)
      (url-retrieve url 't/eww-readable-after-render (list url buffer fn)))))
#+END_SRC

**** Images and wrap long lines

#+BEGIN_SRC emacs-lisp
(after! shr
  ;; don't truncate lines in
  (defun shr-fill-text (text) text)
  (defun shr-fill-lines (start end) nil)
  (defun shr-fill-line () nil)

  ;; not too large images
  (setq shr-use-fonts nil
        shr-max-image-proportion 0.6
        shr-ignore-cache t))
#+END_SRC

**** Hook and keybindings

Some useful eww keybindings

#+BEGIN_SRC emacs-lisp
(after! eww
  (defun t/eww-hook ()
    (map!
     :map evil-normal-state-local-map
     "q" 'quit-window
     "S-TAB" 'shr-previous-link
     "TAB" 'shr-next-link
     "R" 'eww-readable
     "M-p" 'backward-paragraph
     "M-n" 'forward-paragraph
     "s-l" 'eww
     "s" (t/spray-micro-state))))
(add-hook 'eww-mode-hook #'t/eww-hook)
#+END_SRC

*** Hackernews

#+begin_src emacs-lisp
(use-package! hnreader
  :commands (hnreader-news)
  :config
  (set-popup-rule! "^*HN" :ignore t))
#+end_src

*** Reddit

#+begin_src emacs-lisp
(use-package! reddigg
  :commands (reddigg-view-main)
  :config
  (progn
    (set-popup-rule! "^*reddigg" :ignore t)
    (setq reddigg-subs '(emacs minilab homelab homeserver orgmode sffpc archlinux))))
#+end_src

*** Nrk.no

A custom function to fetch a clean view of the current news from nrk.no

#+BEGIN_SRC emacs-lisp :results silent
(defun t/clean-nrk-buffer ()
  (flush-lines "^$")
  ;; clean up lines beginning with dates, e.g. 20. sept...
  (beginning-of-buffer)
  (flush-lines "^[0-9][0-9]\.")

  ;; clean up lines beginning with -
  (beginning-of-buffer)
  (t/cleanup-buffer-whitespace-and-indent)
  (while (re-search-forward "*" nil t)
    ;; kill lines with dates, all these news are new
    (when (string-match-p "^* [0-9][0-9]\." (thing-at-point 'line))
      (kill-line) (forward-line) (join-line))
    ;; change * to -
    (replace-match "\n-")
    ;; highlight the line
    (add-text-properties (point-at-bol) (point-at-eol) '(face outline-4)))

  (beginning-of-buffer)

  ;; kill more lines with dates
  (while (re-search-forward "^[0-9][0-9]\." nil t)
    (when (string-match-p "^[0-9][0-9]\. [jfmasond]" (thing-at-point 'line))
      (beginning-of-line) (kill-line) (forward-line) (join-line)))

  ;; remove leading line
  (beginning-of-buffer)
  (kill-line)

  ;;(darkroom-mode)
  (read-only-mode)
  (funcall (t/micro-state (t/prefix-arg-universal?)
                          "n" (cmd! nil
                                        (evil-search "^-" t t)
                                        (evil-ex-nohighlight)
                                        (recenter nil))
                          "p" (cmd! nil
                                        (evil-search "^-" nil t)
                                        (evil-ex-nohighlight)
                                        (recenter nil))
                          "s" (t/spray-micro-state))))
#+END_SRC

Bind it to ~SPC o n~

#+begin_src emacs-lisp
(map!
 :leader
 (:prefix ("o" . "open")
  :desc "Show nrk.no" "n" (cmd! (t/eww-readable "https://www.nrk.no/nyheter/" 't/clean-nrk-buffer))))
#+end_src

** Languages

*** Markdown

Move around like in org, collapsing what is moved away from, expanding what is moved to.

#+begin_src emacs-lisp
(map! :map markdown-mode-map
      "C-c C-p" (cmd!
                 (outline-show-all)
                 (outline-hide-body)
                 (markdown-outline-previous)
                 (outline-show-entry))
      "C-c C-n" (cmd!
                 (outline-show-all)
                 (outline-hide-body)
                 (markdown-outline-next)
                 (outline-show-entry)))
#+end_src

*** Bash

#+begin_src sh :tangle no :results silent
npm install -g bash-language-server
#+end_src

*** Clojure

Adapt cleverparens keys that clash with my M-[hjkl] bindings in [[file:~/.skhdrc][~/.skhdrc]]

#+begin_src emacs-lisp
(after! evil
  (map! :map evil-cleverparens-mode-map
        "C-M-h" 'evil-cp-beginning-of-defun
        "C-M-l" 'evil-cp-end-of-defun
        "C-M-k" 'evil-cp-drag-backward
        "C-M-j" 'evil-cp-drag-forward))
#+end_src

#+begin_src emacs-lisp
(after! (clojure-mode)
  (add-hook! '(clojure-mode-hook
               clojurec-mode-hook
               clojurescript-mode-hook) 'evil-cleverparens-mode)
  (map! :map clojure-mode-map "DEL" #'sp-backward-delete-char))
#+end_src

Holding alt for moving between sexps feel right

#+begin_src emacs-lisp
(after! (clojure-mode)
  (map! :map clojure-mode-map
        :i "M-<right>" #'evil-cp-forward-sexp
        :i "M-<left>" #'evil-cp-backward-sexp)
  (map! :map clojurec-mode-map
        :i "M-<right>" #'evil-cp-forward-sexp
        :i "M-<left>" #'evil-cp-backward-sexp)
  (map! :map clojurescript-mode-map
        :i "M-<right>" #'evil-cp-forward-sexp
        :i "M-<left>" #'evil-cp-backward-sexp))
#+end_src

*** Emacs lisp

#+begin_src emacs-lisp
(after! evil
  (add-hook 'emacs-lisp-mode-hook #'evil-cleverparens-mode))
#+end_src

Show containing parens, when the cursor is inside theme.

#+begin_src emacs-lisp
;; TODO is this working?
(define-advice show-paren-function (:around (fn) fix)
  "Highlight enclosing parens."
  (cond ((looking-at-p "\\s(") (funcall fn))
        (t (save-excursion
             (ignore-errors (backward-up-list))
             (funcall fn)))))
#+end_src

*** Terraform

Highlight terraform plans in ~terraform-mode~ based on their file name.

#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist (cons (concat "^" (t/user-file "Downloads/") "tf_plan_.*") 'terraform-mode))
(after! terraform-mode
  (defun t-tf-plan-hook ()
    (interactive)
    (flycheck-mode -1)
    (when (s-contains-p "tf_plan_" buffer-file-name)
      (beginning-of-buffer)
      (evil-search "^───" t t)
      (call-interactively 'evil-scroll-line-to-top)))
  (add-hook! 'terraform-mode-hook #'terraform-format-on-save-mode)
  (add-hook! 'terraform-mode-hook #'t-tf-plan-hook))
#+end_src

**** Search for terraform resources

#+begin_src emacs-lisp
(defun t/tf-grep ()
  (interactive)
  (+vertico/project-search
   nil
   "^\\(\\<resource\\>\\|\\<output\\>\\|\\<variable\\>\\|\\<module\\>\\|\\<variable\\>\\)#\\(\"[^\"]+\"\\)~ \\(\"[^\"]+\"\\)?~ {"
   ))
#+end_src
**** Eglot ls

#+begin_src sh :tangle no :results silent
case $(uname) in
    Darwin)
        brew install hashicorp/tap/terraform-ls
        ;;
    Linux)
        false
        ;;
esac
#+end_src

#+begin_src emacs-lisp
(after! eglot
  (add-to-list 'eglot-server-programs '((hcl-mode terraform-mode) . ("terraform-lsp")))
  )
#+end_src

*** Kotlin

**** Eglot ls

#+begin_src sh :tangle no :results silent
wget https://github.com/fwcd/kotlin-language-server/releases/download/1.3.1/server.zip -O ~/bin/server.zip
cd ~/bin/
rm -rf server
unzip server.zip
ln -sf server/bin/kotlin-language-server .
#+end_src

#+begin_src emacs-lisp
(after! eglot
  (setq eglot-connect-timeout (* 60 20)))
#+end_src

Redefine kotlin formatter to add ~-l warn~ to make it not insert an INFO log line in every kotlin file you save...

#+begin_src emacs-lisp
(after! format-all
  (define-format-all-formatter ktlint
    (:executable "ktlint")
    (:install (macos "brew install ktlint"))
    (:modes kotlin-mode)
    (:format (format-all--buffer-easy executable "--format" "--stdin" "-l" "warn"))))
#+end_src

*** Python

**** Eglot ls

Create a venv in venv.

#+begin_src sh :tangle no :result no
python3 -m venv ~/.doom.d/test-files/venv
#+end_src

#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist
             (cons (concat "^" (t/user-emacs-file "test-files/*"))
                   (defun t/activate-pyenv-test-files ()
                     (pyvenv-activate (t/user-emacs-file "test-files/venv")))))
#+end_src

Install [[https://github.com/python-lsp/python-lsp-server][a language server]] in the venv.

#+begin_src sh :tangle no
case $(uname) in
    Darwin)
        #pip install python-lsp-server
        pip install 'python-lsp-server[all]'
        ;;
    Linux)
        false
        ;;
esac
#+end_src

*** remark-mode

#+begin_src emacs-lisp
(use-package! remark-mode
  :commands remark-mode
  :init
  (set-popup-rule! "*remark browser*" :ttl nil))
#+end_src

*** Typescript

I need prettier.

#+begin_src sh :tangle no :results silent
npm install -g prettier
#+end_src

#+begin_src emacs-lisp
(setq +format-with-lsp nil)
#+end_src

**** Eglot ls

#+begin_src sh :tangle no :results silent
npm install -g typescript-language-server typescript
#+end_src

#+begin_src emacs-lisp
(after! eglot
  (add-to-list 'eglot-server-programs '((typescript-tsx-mode) . ("typescript-language-server" "--stdio"))))
#+end_src

#+begin_src emacs-lisp
(after! typescript-mode
  (defun t-ts-mode-hook ()
    (electric-indent-mode -1))
  (add-hook! '(typescript-mode-hook typescript-tsx-mode-hook) #'t-ts-mode-hook))
#+end_src

**** Typescript repl with ts-node

Adapt [[file:~/.emacs.d/.local/straight/repos/nodejs-repl.el/nodejs-repl.el::defun nodejs-repl (][what ~nodejs-repl~ does]] using ~ts-node~.

#+begin_src sh :tangle no :results silent
npm install -g ts-node
#+end_src

You need a =tsconfig.json= for the following to work.

#+begin_src sh :tangle no :results silent
cat <<EOF > ~/.doom.d/test-files/tsconfig.json
{
  "compilerOptions": {
    "module": "commonjs",
    "jsx": "react",
    "esModuleInterop": true
  }
}
EOF
#+end_src

Then make doom understand typescript, e.g. when you select something and go =SPC o r=. 

#+begin_src emacs-lisp
(defun t/ts-node-repl ()
  (interactive)
  (pop-to-buffer
   (make-comint "ts-node repl" "ts-node")))
(set-repl-handler! '(typescript-mode typescript-tsx-mode) #'t/ts-node-repl)
#+end_src

Or when you ~eval~ something when the repl is open, e.g. =g r a p= (eval around paragraph). For multiline stuff type =.editor= into the repl before running the command, finish it with =C-c C-d=

#+begin_src emacs-lisp
(set-eval-handler! '(typescript-mode typescript-tsx-mode)
  '((:command     . "ts-node")
    (:exec        . "%c %s")
    (:description . "Run ts-node script")))
#+end_src

=(eval +overlay)= works in elisp, but is not that good for js/ts, so limit it to use the popup instead.

#+begin_src emacs-lisp
(setq +eval-popup-min-lines 0)
#+end_src

** Server mode

Emacs server setup.

This name is used from skhdrc, ~/.emacs.d/emacsclient~, ~/.emacs.d/emacsclient~ and other places. The latter two are linked as ~e~ and ~em~ on the command line, to target the running emacs server instance, to quickly open a file or folder.

I also use this from Alfred, as a quick way of capturing from anywhere.

#+begin_src sh :tangle no :results silent
/usr/bin/env emacsclient -e '(progn (select-frame-set-input-focus (selected-frame)) (org-capture))'
#+end_src

** Terminal
*** Vterm

This is paired with the bash function [[https://github.com/torgeir/dotfiles/blob/5a4909422f0822016a4e3745e919d7ca246872ec/source/functions#L280-L282][vterm_set_directory]] that updates the current working directory for emacs as the vterm path changes.

#+begin_src emacs-lisp
(setq vterm-shell "/usr/bin/env zsh")
#+end_src

#+begin_src emacs-lisp
(after! vterm
  ;; https://github.com/akermu/emacs-libvterm#how-can-i-get-the-directory-tracking-in-a-more-understandable-way
  ;; see dotfiles/source/functions
  (add-to-list
   'vterm-eval-cmds
   '("update-pwd" (lambda (path) (setq default-directory path))))

  (add-to-list
   'vterm-eval-cmds
   '("magit-diff" (lambda (path)
                   (let ((default-directory path))
                     (call-interactively' magit-diff)))))

  (add-to-list
   'vterm-eval-cmds
   '("magit-log" (lambda (path)
                   (let ((default-directory path))
                     (call-interactively' magit-log)))))

  (add-to-list
   'vterm-eval-cmds
   '("magit-status" (lambda (path)
                      (let ((default-directory path))
                        (call-interactively' magit-status))))))
#+end_src

Make pasting from ~consult-yank-from-kill-ring~ actually insert results in ~vterm~.

#+begin_src emacs-lisp
(defun my/insert-for-yank-vterm-shim (orig-fun &rest args)
  (if (eq major-mode 'vterm-mode)
      (let ((inhibit-read-only t))
        (apply #'vterm-insert args))
    (apply orig-fun args)))
#+end_src

#+begin_src emacs-lisp
(advice-add #'+default/newline :around #'my/insert-for-yank-vterm-shim)
(advice-add #'insert-char-preview :around #'my/insert-for-yank-vterm-shim)
(advice-add #'insert-for-yank :around #'my/insert-for-yank-vterm-shim)
#+end_src

Keep evil insert mode cursor after shell commands have run.

#+begin_src emacs-lisp :results silent
(advice-add #'vterm--redraw :after (lambda (&rest args) (evil-refresh-cursor evil-state)))
(advice-add #'vterm--delayed-redraw :after (lambda (&rest args) (evil-refresh-cursor evil-state)))
#+end_src

**** Keybindings

Some keybindings are so engrained I can't live without them.

#+begin_src emacs-lisp
(after! vterm
  ;; ^xe edit-command-line in zshrc
  ;; ^x^e edit-command-line in zshrc
  (map! :map vterm-mode-map
        :i "C-x e" (cmd!
                    (vterm-send-key "x" nil nil t)
                    (vterm-send-key "e" nil nil nil))
        :i "C-x C-e" (cmd!
                      (vterm-send-key "x" nil nil t)
                      (vterm-send-key "e" nil nil t)))
  (map! :map vterm-mode-map
        ;; undo like in the terminal
        :i "C-_" (cmd! (vterm-send-key "_" t nil t))
        :i "M-<return>" (cmd! (vterm-send-key "<return>" nil t nil))
        :i "C-<return>" (cmd! (vterm-send-key "<return>" nil nil t))
        ;; send c-up c-down for dir stack zsh navigation
        :i "C-<up>" (cmd! (vterm-send-key "<up>" nil nil t))
        :i "C-<down>" (cmd! (vterm-send-key "<down>" nil nil t))
        :i "s-<up>" (cmd! (vterm-send-key "<up>" nil nil t))
        :i "s-<down>" (cmd! (vterm-send-key "<down>" nil nil t)))
  (map! :map vterm-mode-map
        :m "C-a" 'vterm-send-C-a
        :m "M-<backspace>" 'vterm-send-C-w
        :i "M-<backspace>" 'vterm-send-C-w
        :i "C-h" 'vterm-send-C-h))
#+end_src

Remove ~C-l~ in normal mode, so that ~recenter-top-bottom~ works. Bring it back in insert mode.

#+begin_src emacs-lisp
(after! vterm
  (map! :map vterm-mode-map "C-l" nil)
  (map! :map vterm-mode-map :i "C-l" 'vterm-clear))
#+end_src

*** Terminal from everywhere with s-return
:PROPERTIES:
:ID:       607b5e84-af21-4da9-80a9-8a44c2d12f0b
:END:

Make super+enter create a vterm terminal window inside emacs.

#+begin_src emacs-lisp
(map! :gn [s-return]
      (cmd!
       (if (eq major-mode 'dired-mode)
           (let* ((selected-dir (dired-get-marked-files t current-prefix-arg))
                  (selected-path (concat default-directory (car selected-dir)))
                  (default-directory
                    (if (file-directory-p selected-path)
                        selected-path
                      (file-name-directory selected-path))))
             (+vterm/here t))
         (+vterm/here t))))
#+end_src

Goes great with [[file:~/dotfiles/skhdrc::cmd - return \[][these lines from ~/.skhdrc]], that make super+enter create a terminal from other apps.

*** Close comint buffers with c-d

#+begin_src emacs-lisp
(map! :map comint-mode-map
      :n "C-d" (cmd! (call-interactively 'evil-scroll-down))
      :i "C-d" #'t/volatile-kill-buffer-and-window)
#+end_src

#+begin_src emacs-lisp
(after! cider
  (map! :map cider-repl-mode-map
        :n "C-d" (cmd! (call-interactively 'evil-scroll-down))
        :i "C-d" #'t/volatile-kill-buffer-and-window))
#+end_src

*** Disable some modes in vterm

It messes with evil movement, e.g. when attemting to jump somewhere and perform an action on an evil text object, like ~yiw~.

#+begin_src emacs-lisp
(after! which-key
  (defun t/delayed-which-key (_ _)
    "Suggested in https://github.com/justbur/emacs-which-key/issues/243"
    (cond
     ((eq major-mode 'vterm-mode) 2)
     (t nil)))
  (add-hook! 'which-key-delay-functions #'t/delayed-which-key))

(defun t/vterm-mode-hook ()
  (eros-mode -1)
  (buffer-face-mode t))
(add-hook! 'vterm-mode-hook #'t/vterm-mode-hook)
#+end_src

** VC
*** Ediff

Sometimes you need both changes.

#+begin_src emacs-lisp
(after! ediff
  (defun t/bind-ediff-use-both ()
    (define-key ediff-mode-map "d" 't/ediff-use-both))
  (add-hook! 'ediff-keymap-setup-hook #'t/bind-ediff-use-both))
#+end_src

*** Magit and Forge

Useful magit keybindings:

- ~S-SPC~ :: preview commit
- ~gj~ :: next and preview
- ~j~ :: next

~magit-log-arguments~ and the like are not ment to be used like a list you add args to, instead set options in the magit transient buffer by toggling them and saving it with ~c-x c-s~.

#+begin_src emacs-lisp
(after! magit
  (setq git-commit-summary-max-length 72 ;; like github
        magit-display-buffer-function 'magit-display-buffer-same-window-except-diff-v1)

  (defun t/commit-mode-hook ()
    (add-to-list 'whitespace-style 'trailing)
    (whitespace-mode 1))
  (add-hook! 'git-commit-mode-hook 't/commit-mode-hook)

  (set-popup-rule! "^magit:" :ignore t)
  (set-popup-rule! "^magit-revision" :side 'right :size 0.5))
#+end_src

Extend leader map with ~gn~ and ~gN~, for navigating hunks, the g] and g[ bindings never made sense to me. And ~gca~ for amending.

#+begin_src emacs-lisp
(map!
 :after git-gutter
 :leader
 (:prefix-map
  ("g" . "git")
  (:when (modulep! :ui vc-gutter)
    :desc "Stage hunk"            "s" (cmd! (let ((git-gutter:ask-p nil))
                                              (git-gutter:stage-hunk)))
    :desc "Jump to next hunk"     "n" (cmd! (call-interactively 'git-gutter:next-hunk)
                                            (call-interactively 'evil-scroll-line-to-center))
    :desc "Jump to previous hunk" "N" (cmd! (call-interactively 'git-gutter:previous-hunk)
                                            (call-interactively 'evil-scroll-line-to-center)))))
#+end_src

#+begin_src emacs-lisp
(map!
 :after magit
 :leader
 (:prefix-map
  ("g" . "git")
  (:when (modulep! :tools magit)
    :desc "Diff dwim"             "d" #'magit-diff-dwim
    :desc "Ediff dwim"            "e" #'magit-ediff-dwim
    :desc "Visit pulls"           "p" #'t/visit-git-link-pulls
    :desc "Push"                  "P" #'magit-push
    (:prefix ("c" . "create")
     :desc "Ammend"               "a"   #'magit-commit-amend))))
#+end_src

I have been trying to get used to magit in evil mode for a while now. But the magit-process-buffer keybinding is crazy on a norwegian keyboard, so this brings back the binding from the emacs mode magit.

#+begin_src emacs-lisp
(map!
 :map magit-status-mode-map
 :desc "Show process buffer" :n "$" #'magit-process-buffer)
#+end_src

Colorz.

#+begin_src emacs-lisp
(after! magit
  (set-face-attribute 'magit-diff-hunk-heading nil :background "#513d5b" :foreground "#07010E")
  (set-face-attribute 'magit-diff-hunk-heading-highlight nil :background "#ED60BA" :foreground "#01010E" :weight 'bold)
  (set-face-attribute 'magit-diff-revision-summary nil :inherit 'magit-diff-hunk-heading :foreground "#ED60BA"))
#+end_src

*** Blamer mode

Bound to ~SPC t b~.

#+begin_src emacs-lisp
(use-package! blamer
  :commands (blamer-mode)
  :custom
  (blamer-idle-time 0.5)
  (blamer-min-offset 50)
  (blamer-prettify-time-p t)
  (blamer-entire-formatter "%s")
  (blamer-author-formatter " %s")
  (blamer-datetime-formatter " [%s] ")
  (blamer-commit-formatter "✎ %s")
  :custom-face
  (blamer-face ((t :foreground "#7a88cf" :background nil :height 100 :italic t))))
#+end_src

** Applications

*** Artist

#+begin_src emacs-lisp
(defun t/artist-mode ()
  (interactive)
  (if (and (boundp 'artist-mode)
           artist-mode)
      (progn
        (artist-mode-off)
        (evil-normal-state))
    (progn
      (switch-to-buffer "*scratch*")
      (evil-insert-state)
      (artist-mode t))))

(after! artist
  (add-hook! 'artist-mode-hook
    (defun t/artist-mode-hook ()
      (map!
       :map evil-insert-state-local-map "q" 'artist-mode-off
       :map evil-normal-state-local-map "q" 'artist-mode-off))))

(map!
 :leader
 (:prefix-map
  ("z" . "misc")
  (:prefix
   ("z" . "artist")
   (:when t
     :desc "Enable"          "t" 't/artist-mode
     :desc "Draw: pen"       "p" 'artist-select-op-pen-line
     :desc "Draw: line"      "l" 'artist-select-op-line
     :desc "Draw: rectangle" "r" 'artist-select-op-rectangle
     :desc "Draw: circle"    "c" 'artist-select-op-circle
     :desc "Draw: ellips"    "e" 'artist-select-op-ellipse
     :desc "Draw: square"    "s" 'artist-select-op-square))))
#+end_src

*** Elfeed RSS

Setup.

#+begin_src emacs-lisp
(after! elfeed
  (setq rmh-elfeed-org-files '("~/Dropbox/org/feeds.org")
        elfeed-goodies/entry-pane-position 'bottom
        elfeed-search-filter "@2-week-ago -news -tech +unread"))
#+end_src

Switch around the refresh mappings for more useful defaults.

#+begin_src emacs-lisp
(after! elfeed
  (map!
   :map elfeed-search-mode-map
   :n "!" #'elfeed-search-untag-all-unread
   :n "?" #'elfeed-search-tag-all-unread
   ;; switcharoo
   :n "gR" #'elfeed-search-update--force
   :n "gr" #'elfeed-search-fetch))
#+end_src

#+begin_src emacs-lisp
(after! elfeed
  ;;(setq elfeed-search-mode-hook nil)
  (add-hook 'elfeed-search-mode-hook
            (defun t/hook-elfeed-search-mode-hook ()
              (show-paren-mode -1)
              ;; don't wrap long lines
              (visual-line-mode -1)
              (toggle-truncate-lines 1))))
#+end_src

Make reading smoother. Turn on =olivetti-mode= to center content. Wrap lines. Bind ~n~, ~p~ for nav, that skips to the next item on reaching the end.

#+begin_src emacs-lisp
(after! elfeed
  ;;(setq elfeed-show-mode-hook nil)
  (add-hook 'elfeed-show-mode-hook
            (defun t/hook-elfeed-show-mode-hook ()
              (olivetti-mode)
              (run-at-time "0 sec" nil (cmd! (visual-line-mode)))
              (t/start-spray-micro-state (lambda () (equal major-mode 'elfeed-show-mode))))))
#+end_src

Show the mouseover text for xkcd comics by moving to the image and fetching the ~'shr-alt~ text property that holds the mouse over text. Insert it in the buffer on the next tick, to wait for the image to appear first.

#+begin_src emacs-lisp
(after! elfeed
  (add-hook 'elfeed-show-mode-hook
            (defun t/elfeed-show-xkcd-mouseover-hook ()
             (run-at-time "0 sec" nil
                          (cmd!
                           (when (s-equals-p "xkcd.com" (car (elfeed-entry-id elfeed-show-entry)))
                             (save-excursion
                               (forward-line)
                               (read-only-mode -1)
                               (when-let ((text (get-text-property (point) 'shr-alt)))
                                 (goto-char (point-max))
                                 (insert "\n\n")
                                 (insert text))
                               (read-only-mode 1))))))))
#+end_src

*** Calendar

Ready some fonts that stand out.

#+begin_src emacs-lisp
(after! calendar
  (copy-face font-lock-comment-face 'calendar-week-face)
  (copy-face font-lock-string-face 'calendar-today-face)
  (set-face-attribute 'holiday nil :foreground "VioletRed1" :weight 'bold :background nil)
  (set-face-attribute 'calendar-today-face nil :weight 'bold :background nil)
  (set-face-attribute 'calendar-week-face nil :foreground "VioletRed4"))
#+end_src

Weeks on start on monday in Norway, and weeks have numbers. I also like holidays.

#+begin_src emacs-lisp
(after! calendar
  (setq calendar-date-style 'iso
        calendar-week-start-day 1
        calendar-mark-holidays-flag t
        calendar-today-marker 'calendar-today-face
        calendar-intermonth-header '(propertize "w" 'font-lock-face 'calendar-week-face)
        calendar-intermonth-text '(propertize
                                   (format "%2d" (car
                                                  (calendar-iso-from-absolute
                                                   (calendar-absolute-from-gregorian
                                                    (list month day year)))))
                                   'font-lock-face
                                   'calendar-week-face)))
#+end_src

Translate days and seasons to norwegian.

#+begin_src emacs-lisp
(after! calendar
  (setq-hook! 'calendar-mode-hook
    calendar-day-name-array ["Søndag" "Mandag" "Tirsdag" "Onsdag" "Torsdag" "Fredag" "Lørdag"]
    calendar-day-header-array ["sø" "ma" "ti" "on" "to" "fr" "lø"]
    solar-n-hemi-seasons '("Vårjevndøgn"  "Sommersolverv" "Høstjevndøgn" "Vintersolherv")))
#+end_src

Mark today when scrolling past it.

#+begin_src emacs-lisp
(after! calendar
  (add-hook 'calendar-today-visible-hook 'calendar-mark-today))
#+end_src

Norwegian holidays.

#+begin_src emacs-lisp
(after! calendar
  (add-hook 'calendar-initial-window-hook
            (defun t/calendar-initial-window-hook ()
              (require 'calendar-norway)
              (setq calendar-holidays
                    (append
                     calendar-norway-raude-dagar
                     calendar-norway-andre-merkedagar
                     calendar-norway-dst
                     '((holiday-fixed 3 17 "St. Patricksdag")
                       (holiday-fixed 10 31 "Halloween")
                       (holiday-float 11 4 4 "Thanksgiving"))))
              (calendar-redraw))))
#+end_src

Evil like navigation.

#+begin_src emacs-lisp
(after! calendar
  (add-hook! 'calendar-mode-hook
             (defun t/calendar-mode-hook ()
               (map!
                :map calendar-mode-map
                :m "H"   #'calendar-scroll-left
                :m "L"   #'calendar-scroll-right))))
#+end_src

*** Re-builder

#+begin_src emacs-lisp
(after! re-builder
  (setq reb-re-syntax 'rx)
  (defvar t-regex-mode nil "reb-mode on or not"))

(defun t/toggle-regex-mode ()
  (interactive)
  (if t-regex-mode (reb-quit) (re-builder))
  (setq t-regex-mode (not t-regex-mode)))
#+end_src

*** Twitter

#+begin_src emacs-lisp
(after! twittering-mode
  (setq twittering-initial-timeline-spec-string '(":mentions" ":home")
        twittering-request-confirmation-on-posting t
        twittering-use-master-password t))
#+end_src

*** Email

Settings from [[file:~/.emacs.d/modules/email/mu4e/README.org][modules/email/mu4e/README.org]]

Inspiration:
- https://pragmaticemacs.wordpress.com/2016/03/22/migrating-from-offlineimap-to-mbsync-for-mu4e/
- https://macowners.club/posts/email-emacs-mu4e-macos/
- https://www.reddit.com/r/emacs/comments/8q84dl/tip_how_to_easily_manage_your_emails_with_mu4e/
- https://www.reddit.com/r/emacs/comments/bfsck6/mu4e_for_dummies/
- https://www.djcbsoftware.nl/code/mu/mu4e/Multiple-accounts.html
- https://f-santos.gitlab.io/2020-04-24-mu4e.html
- https://www.erichgrunewald.com/posts/setting-up-gmail-in-doom-emacs-using-mbsync-and-mu4e/
- https://blog.leonardotamiano.xyz/posts/mu4e-setup/

**** Mu4e

#+begin_src emacs-lisp
(after! mu4e

  (set-popup-rule! "^*mu4e-main" :ignore t)
  (set-popup-rule! "^*mu4e-headers" :ignore t) ;; :side 'right :size 0.8

  (add-hook! 'mu4e-main-mode-hook (defun t/mu4e-main-mode-hook ()
                                    (interactive)
                                    ;; TODO torgeir
                                    (toggle-truncate-lines 1)
                                    (visual-line-mode 0)))

  (setq mu4e-maildir "~/.maildir"

        mu4e-update-interval (* 5 60)
        mu4e-context-policy 'ask
        mu4e-attachment-dir "~/Desktop"
        mu4e-mu-binary (executable-find "mu")
        mu4e-get-mail-command (concat (executable-find "mbsync") " -a")
        mu4e-headers-fields '((:account-stripe . 1)
                              (:human-date . 12)
                              (:flags . 6)
                              (:from . 25)
                              (:to . 25)
                              (:subject))

        ;; rename files when moving - needed for mbsync:
        mu4e-change-filenames-when-moving t
        mu4e-maildir-shortcuts '(("/gmail/INBOX" . ?g)
                                 ("/gmail/[Gmail]/Sent Mail" . ?G)
                                 ("/junk/INBOX" . ?j)
                                 ("/junk/[Gmail]/Sent Mail" . ?J)))

  (add-to-list 'mu4e-bookmarks '(:name "Gmail" :query "maildir:/gmail/INBOX" :key ?g))
  (add-to-list 'mu4e-bookmarks '(:name "Junk"  :query "maildir:/junk/INBOX" :key ?j))

  (setq mail-user-agent 'mu4e-user-agent)

  (setq +mu4e-gmail-accounts `((,user-mail-address   . "/gmail")
                               (,user-mail-address-2 . "/junk")))

  (set-email-account!
   "gmail"
   `((mu4e-sent-folder       . "/gmail/[Gmail]/Sent Mail")
     (mu4e-drafts-folder     . "/gmail/[Gmail]/Drafts")
     (mu4e-trash-folder      . "/gmail/[Gmail]/Trash")
     (mu4e-compose-signature . "\nT")
     (user-full-name         . ,user-full-name)
     (user-mail-address      . ,user-mail-address)
     (message-sendmail-extra-arguments . ("--read-envelope-from" "--account=gmail"))
     (org-msg-signature      . "\n\n#+begin_signature\n--\n\nT\n#+end_signature"))
   t)

  (set-email-account!
   "junk"
   `((mu4e-sent-folder       . "/junk/[Gmail]/Sent Mail")
     (mu4e-drafts-folder     . "/junk/[Gmail]/Drafts")
     (mu4e-trash-folder      . "/junk/[Gmail]/Trash")
     (mu4e-compose-signature . "\ntorg")
     (user-full-name         . "torg")
     (user-mail-address      . ,user-mail-address-2)
     (message-sendmail-extra-arguments . ("--read-envelope-from" "--account=junk"))
     (org-msg-signature      . "\n\n#+begin_signature\n--\n\ntorg\n#+end_signature"))
   t)

  (setq sendmail-program (executable-find "msmtp")
        send-mail-function #'smtpmail-send-it
        message-sendmail-f-is-evil t
        message-send-mail-function #'message-send-mail-with-sendmail))
#+end_src

***** Evilify

#+begin_src emacs-lisp
(after! mu4e
  ;; TODO until evil-collection is updated
  (evil-define-key* '(emacs) mu4e-main-mode-map (kbd "SPC") 'doom/leader)
  (evil-define-key* '(emacs) mu4e-headers-mode-map (kbd "SPC") 'doom/leader))
#+end_src

***** Don't insert line breaks, softwrap

#+begin_src emacs-lisp
(after! mu4e
  (add-hook 'mu4e-compose-mode-hook 'turn-off-auto-fill))
#+end_src

***** Don't wrap lines in message overview

#+begin_src emacs-lisp
(after! mu4e
  (add-hook 'mu4e-headers-mode-hook
            (defun t/mu4e-headers-mode-hook ()
              (visual-line-mode -1)
              (show-paren-mode -1))))
#+end_src

***** Cc Bcc

#+begin_src emacs-lisp
(after! mu4e
  (add-hook 'mu4e-compose-mode-hook
            (defun t/mu-add-cc-and-bcc ()
              "My Function to automatically add Cc & Bcc: headers."
              (save-excursion (message-add-header "Cc:\n"))
              (save-excursion (message-add-header "Bcc:\n")))))
#+end_src

***** Wrap long lines in emails

Why the timeout is needed? I cannot for the life of me make ~visual-line-mode~ work in mu4e without this. Also get rid of trailing whitespace after, usually cleans stuff up.

#+begin_src emacs-lisp
(after! mu4e
  (add-hook 'mu4e-view-mode-hook
            (defun t/mu4e-view-mode-hook ()
              ;; TODO until evil-collection is updated
              (evil-normal-state)
              (t/start-spray-micro-state)
              (run-at-time "2 sec" nil (cmd! (visual-line-mode)
                                             (read-only-mode -1)
                                             (delete-trailing-whitespace)
                                             (read-only-mode 1))))))
#+end_src

[[https://github.com/doomemacs/doomemacs/issues/970][Related struggles]].

***** Remove background color in emails

#+begin_src emacs-lisp
(after! mu4e
  (require 'mu4e-contrib)
  (setq mu4e-html2text-command 'mu4e-shr2text)
  (setq shr-color-visible-luminance-min 60)
  (setq shr-color-visible-distance-min 5)
  (setq shr-use-colors nil)
  (advice-add #'shr-colorize-region :around (defun shr-no-colourise-region (&rest ignore))))
#+end_src

**** Setup

***** Create the folders

The subfolder ~gmail~ is what makes the mu4e setting above need ~/gmail/~ in their path

#+begin_src sh :results silent :tangle no
mkdir -p ~/.maildir/{gmail,junk,certificates}
#+end_src

***** Export certificates

On os x: ~Keychain Access -> System Roots -> Certificates -> select all -> Shift+cmd+E~ to ~~/.maildir/certificates/certificates.pem~

On arch: ~/etc/ssl/certs/ca-certificates.crt~

***** Delete to Trash

Gmail setting to remove deleted emails from inbox and move them in ~[Gmail]/Trash~.

#+begin_quote
Go to Gmail IMAP/POP settings (in normal view; the options are not available in HTML view) and set "When a message is marked as deleted" to "Move to trash" or "Immediately delete." Also set "Auto-Expunge off."
#+end_quote

**** Smtp

=brew install msmtp=

Create ~.msmtprc~ config for sending email

#+begin_src emacs-lisp
(after! f
  (f-write-text
   (concat "defaults
auth on
port 587
protocol smtp
tls on
tls_starttls on

account gmail
host smtp.gmail.com
from " (getenv "USER_EMAIL") "
user " (replace-regexp-in-string "@gmail.com" "" (getenv "USER_EMAIL")) "
passwordeval \"gpg -q --for-your-eyes-only --no-tty --logger-file /dev/null --batch -d ~/.authinfo.gpg | awk '/machine smtp\\.gmail\\.com login "
(replace-regexp-in-string "\\." "\\\\." (getenv "USER_EMAIL"))
" port 587/ {print $NF}'\"

account junk
host smtp.gmail.com
from " (getenv "USER_EMAIL_2") "
user " (replace-regexp-in-string "@gmail.com" "" (getenv "USER_EMAIL_2")) "
passwordeval \"gpg -q --for-your-eyes-only --no-tty --logger-file /dev/null --batch -d ~/.authinfo.gpg | awk '/machine smtp\\.gmail\\.com login "
(replace-regexp-in-string "\\." "\\\\." (getenv "USER_EMAIL_2"))
" port 587/ {print $NF}'\"
")
   'utf-8 (t/user-file ".msmtprc")))
#+end_src

**** Mbsync

=brew install isync=

#+begin_src emacs-lisp
(after! f
  (f-write-text
   (concat "
 # gmail ========================
 IMAPAccount gmail
 Host imap.gmail.com
 User " (getenv "USER_EMAIL") "
 PassCmd \"gpg -q --for-your-eyes-only --no-tty --logger-file /dev/null --batch -d ~/.authinfo.gpg | awk '/machine imap\\.gmail\\.com login "
 (replace-regexp-in-string "\\." "\\\\." (getenv "USER_EMAIL"))
 " port 993/ {print $NF}'\"
 Port 993
 SSLType IMAPS
 SSLVersions TLSv1.2
 AuthMechs PLAIN
 SystemCertificates no
 CertificateFile " (if IS-MAC
                       "~/.maildir/certificates/certificates.pem"
                     "/etc/ssl/certs/ca-certificates.crt") "

 IMAPStore gmail-remote
 Account gmail

 MaildirStore gmail-local
 SubFolders Verbatim
 Path ~/.maildir/gmail/
 Inbox ~/.maildir/gmail/INBOX

 Channel gmail
 Far :gmail-remote:
 Near :gmail-local:
 Patterns * ![Gmail]* \"[Gmail]/Sent Mail\" \"[Gmail]/Trash\"
 Create Near
 Sync All
 Expunge Both
 SyncState *

 # junk =========================
 IMAPAccount junk
 Host imap.gmail.com
 User " (getenv "USER_EMAIL_2") "
 PassCmd \"gpg -q --for-your-eyes-only --no-tty --logger-file /dev/null --batch -d ~/.authinfo.gpg | awk '/machine imap\\.gmail\\.com login "
 (replace-regexp-in-string "\\." "\\\\." (getenv "USER_EMAIL_2"))
 " port 993/ {print $NF}'\"
 Port 993
 SSLType IMAPS
 SSLVersions TLSv1.2
 AuthMechs PLAIN
 SystemCertificates no
 CertificateFile " (if IS-MAC
                       "~/.maildir/certificates/certificates.pem"
                     "/etc/ssl/certs/ca-certificates.crt") "

 IMAPStore junk-remote
 Account junk

 MaildirStore junk-local
 SubFolders Verbatim
 Path ~/.maildir/junk/
 Inbox ~/.maildir/junk/INBOX

 Channel junk
 Far :junk-remote:
 Near :junk-local:
 Patterns * ![Gmail]* \"[Gmail]/Sent Mail\" \"[Gmail]/Trash\"
 Create Near
 Sync All
 Expunge Both
 SyncState *")
   'utf-8 (t/user-file ".mbsyncrc")))
#+end_src

Then sync it with =mbsync -aV=.

**** Mu

First time =mu= initialization

#+begin_src emacs-lisp :tangle no
(shell-command-to-string
 (concat "mu init -m ~/.maildir"
         " --my-address " (getenv "USER_EMAIL")
         " --my-address " (getenv "USER_EMAIL_2")
         " && mu index"))
#+end_src

** Site lisp

Useful elisp I committed, or decided to work on.

#+begin_src emacs-lisp
(after! org
  (use-package! ox-gfm
    :commands org-export-dispatch
    :load-path "site-lisp/ox-gfm/"))
#+end_src

#+begin_src emacs-lisp
;; TODO why is ~/.doom.d, tho
(use-package! consult-async
  :commands consult-web-search
  :load-path "~/.doom.d/site-lisp/consult-async/")
#+end_src

TODO idle highlight in visible buffers

#+begin_src emacs-lisp
;; don't use this for large files, e.g. like 15MB, it really brings emacs to a stall
(use-package! nxml-eldoc
  :load-path "~/.doom.d/site-lisp/nxml-eldoc/"
  :commands turn-on-nxml-eldoc)
#+end_src

#+begin_src emacs-lisp
(use-package! json-path-eldoc
  :commands turn-on-json-path-eldoc
  :init (add-hook! 'json-mode-hook 'turn-on-json-path-eldoc)
  :load-path "~/.doom.d/site-lisp/json-path-eldoc/")
#+end_src

#+begin_src emacs-lisp
(use-package! chatgpt
  :commands (t/chatgpt-init
             chatgpt-prompt
             chatgpt-fix-region
             chatgpt-explain-region
             chatgpt-summarize-region
             chatgpt-summarize-region-and-replace
             chatgpt-gen-tests-for-region
             chatgpt-optimize-region
             chatgpt-refactor-region
             chatgpt-prompt-region
             chatgpt-prompt-region-and-replace)
  :init
  (progn
    (map! :leader
          (:prefix
           ("o" . "open")
           (:prefix-map
            ("g" . "chatgpt")
            (:when t
              :desc "init" "i" #'t/chatgpt-init
              :desc "prompt" "p" #'chatgpt-prompt
              :desc "fix" "f" #'chatgpt-fix-region
              :desc "explain" "e" #'chatgpt-explain-region
              :desc "gen-tests" "t" #'chatgpt-gen-tests-for-region
              :desc "optimize" "o" #'chatgpt-optimize-region
              :desc "refactor" "r" #'chatgpt-refactor-region
              :desc "prompt region" "P" #'chatgpt-prompt-region
              :desc "replace" "R" #'chatgpt-prompt-region-and-replace
              :desc "summarize" "s" #'chatgpt-summarize-region
              :desc "summarize replace" "S" #'chatgpt-summarize-region-and-replace
              ))))
    (defun t/chatgpt-init ()
      (interactive)
      (if (not (getenv "OPENAI_API_KEY"))
          (progn
            (setenv "OPENAI_API_KEY" (t/1p "q2tt4yocntwwx326qck5uqqft4"))
            (message "Openai: Key set."))
        (message "Openai: Key already set.")))
    (let ((p (+popup-make-rule "^\\*ChatGPT" '(:size 0.45 :side right))))
      (setq display-buffer-alist (cdr display-buffer-alist))
      (add-to-list 'display-buffer-alist p)))
  :load-path "~/.doom.d/site-lisp/chatgpt/")
#+end_src

** Bugfixes

*** compat-assoc compat-assoc-delete-all not defined?

#+begin_src emacs-lisp
(when is-linux
  (defun compat-assoc (&optional a b c) nil))
#+end_src

#+begin_src emacs-lisp
(when is-linux
  (defun compat-assoc-delete-all (&optional a b c) nil))
#+end_src

** Stuff to test

*** Run in every file opened

#+begin_src emacs-lisp
(add-hook 'find-file-hook
          (defun t/in-every-file ()
            ;;(when (string= (file-name-extension buffer-file-name) "ts") (typescript-mode))
            ))
#+end_src

*** Sticky buffer mode

Useful e.g. to make dired act like a directory tree sidebar

#+begin_src emacs-lisp :results silent
(define-minor-mode sticky-buffer-mode
  "Make the current window always display this buffer."
  nil " sticky" nil
  (set-window-dedicated-p (selected-window) sticky-buffer-mode)
  (setq window-size-fixed (if sticky-buffer-mode 'width nil)))
#+end_src

*** gh run watch

#+begin_src emacs-lisp :results silent
(defun t/gha ()
  (interactive)
  (let ((current-prefix-arg 1))
    ;; (call-interactively '+vterm/here)
    (call-interactively '+vterm/toggle))
  (term-send-raw-string "gh run watch\C-m"))
#+end_src

*** xref jump to selection

#+begin_src emacs-lisp
(comment

 (progn

   (require 'xref)
   (let ((l (xref-location-marker
             (xref-make-file-location
              (t/user-emacs-file "test-files/index.js")
              27
              11))))
     (xref--show-pos-in-buf l (marker-buffer l))))
 )
#+end_src

** Fun stuff

*** Embark org ToC

Type ~mgg~ to run =consult-org-heading=, then ~C-c C-;~ to =embark-export=.

*** Macro numbered list

Type ~qq~ to record a macro to ~q~. Move to where you want the number and press ~C-x C-k C-i~. Move to the next line start to make the macro repeatble. Type ~q~. Undo. Select the list and hit ~@q~.

- one
- two
- three

** Someday
:PROPERTIES:
:header-args:emacs-lisp: :lexical t :results silent
:END:

~(doom-mark-buffer-as-real-h)~ can make scratch buffer navigable with ~s-j/k~

*** deno-emacs                         :pocket:deno:emacs:

June 18, 2021 at 12:12PM
https://ift.tt/36jQLT2

deno-fmt is function that formats the current buffer on save with deno fmt. The package also exports a minor mode that applies (deno-fmt) on save. Feel free to replace typescript-mode / js2-mode in the following with your TypeScript/JavaScript mode of choice.

*** STARTED Edit server custom keybinding

Insane hack to make ~C-c C-c~ accept and ~C-c C-k~ exit when running ~^x^e~ in a vterm terminal.

#+begin_src emacs-lisp
(advice-remove #'server-done :after)
(advice-remove #'server-execute :after)
(advice-add #'server-execute :after
            (defun t/after-server-execute ()
             (message "t: server edit")
             (global-set-key "\C-c\C-c" (cmd! (save-buffer) (server-edit)))
             (global-set-key "\C-c\C-k" (cmd!
                                         (global-unset-key "\C-c\C-c")
                                         (global-unset-key "\C-c\C-k")
                                         (t/volatile-kill-buffer)))))
(advice-add #'server-done :after
            (defun t/after-server-done ()
             (message "t: server done")
             (global-unset-key "\C-c\C-c")
             (global-unset-key "\C-c\C-k")))
#+end_src

*** STARTED Mastodon

#+begin_src emacs-lisp
(after! mastodon
  (setq mastodon-instance-url "https://fosstodon.org"
        mastodon-active-user "@torgeir@fosstodon.org")
  (map! :map mastodon-mode-map
        :n "q" #'+workspace/delete
        :n "j" (cmd!
                (mastodon-tl--goto-next-toot)
                (recenter-top-bottom 0))
        :n "k" (cmd!
                (mastodon-tl--goto-prev-toot)
                (recenter-top-bottom 0))))
#+end_src

